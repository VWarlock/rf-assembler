;
; Source van RF-Assembler
;
; Correctie van V2.2
; VALEXP/VALTRM was fout
; JP (HL),(IX),(IY) fout
;
;
ADD1 EQU 3271
ADD2 EQU 62819
ADD3 EQU 19370
ADD4 EQU 40014
ADD5 EQU 12837
ADD6 EQU 30777
ADD7 EQU 8352
;
 ORG 100H
;
DISKLINK EQU 0F323H
;
BELL EQU 7
;
MSXBAS EQU 409BH
BDOS EQU 5
ROMDI EQU 0F36BH
CSRY EQU 0F3DCH
CSRX EQU 0F3DDH
BUF EQU 0F55EH
;
LBLSP128 EQU 4000H
LBLSP64 EQU 10802
;
BRKPTMAX EQU 8
;
MXLENEXP EQU 60
MAXNEST EQU 6
MAXPSD EQU 7
WAITVAL EQU 10
;
START JP INIT
;
TESTLOC NOP 
;
LABELSPC DEFS 2
LBLPAGE NOP 
LBLADR DEFS 2
LBLMAX DEFS 2
SRCADR DEFS 2
HIMEM DEFS 2
STACK DEFS 2
LBLPT1 DEFS 2
LBLBUF DEFS 8
VARCNT NOP 
VARBUF DEFS 4
EXPCNT NOP 
LENEXP NOP 
EXPBUF DEFS MXLENEXP+MXLENEXP+8
ASMCNT EQU BUF+128; !!
ASMBUF EQU BUF+129; !!
VALBUF DEFS 18
INDEXT NOP 
INDEX NOP 
DISMOD NOP 
CNSTIF NOP 
DISNMB NOP 
DISADR DEFS 2
LSTADR DEFS 2
INPBUF DEFS 256
BUFPNT DEFS 2
OUTBUF DEFS 128
FRELBL DEFS 2
CNTWAIT NOP 
LINCNT DEFS 2
LINADR DEFS 2
ENDTXT DEFS 2
BLOKLN DEFS 2
PASCNT DEFS 2
ASMTYP NOP 
LBLCNT DEFS 2
MINLIN DEFS 2
;
DRIVENO NOP 
FILENAME DEFS 8
EXTENS DEFS 3
SOURCEXT DEFS 3
FCB DEFS 40
 DEFS 10
DSKCNT NOP 
TRADDR DEFS 2
FILETYPE NOP 
LINKFLAG NOP 
;
RPC DEFS 2
RSP DEFS 2
 DEFS 8
RNORMAL DEFS 10
RAF DEFS 2
;
PCCOPY DEFS 2
SPCOPY DEFS 2
;
STACKPTR DEFS 2
;
INTFLAG DEFB 0FFH
;
INTBUF NOP 
STEPBUF DEFS 4
 JP COMDSP2
STEPCHAR NOP 
;
BRKPTCNT NOP 
BRKPTTBL DEFS 3*BRKPTMAX;BRKPTMAX BREAKPTS
TEMPMEM DEFS 2
BREAKFLG NOP 
;
PRINTER NOP 
ERROR NOP 
INSRTFLG NOP 
BRACKETS NOP 
NESTING NOP 
STOPEXP NOP 
THISOPR NOP 
NEXTOPR NOP 
ASBRACK NOP 
PRIORITY NOP 
;
DEFLINE DEFS 2
;
IMEDIATE NOP 
;
LINKCOPY DEFS 2
;
BREAKX PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 9CH
 POP HL
 POP DE
 POP BC
 JR Z,BREAK2
 CALL CHGET
 CP 3
 SCF 
 RET Z
 CALL CHGET
 CP 3
 SCF 
 RET Z
 JR BREAKX
BREAK2 CP A
 RET 
;
DISKMA PUSH AF
 LD A,','
 CALL PUTBUF
 POP AF
 RET 
;
CHGMOD RST 30H
 DEFB 0
 DEFW 5FH
 RET 
;
CHGET PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 9FH
 POP HL
 POP DE
 POP BC
 RET 
;
CHPUT PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 0A2H
 POP HL
 POP DE
 POP BC
 POP AF
 RET 
;
LPTOUT PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 0A5H
 POP HL
 POP DE
 POP BC
 RET 
;
LPTSTT PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 0A8H
 POP HL
 POP DE
 POP BC
 RET 
;
CHGCAP PUSH BC
 PUSH DE
 PUSH HL
 RST 30H
 DEFB 0
 DEFW 132H
 POP HL
 POP DE
 POP BC
 RET 
;
CHPUT1 CP 13
 JR NZ,CHPUT3
 CALL CHPUT2
 LD A,10
 JP CHPUT2
CHPUT3 CP 8
 JP NZ,CHPUT2
 LD A,7FH
 JP CHPUT2
;
CHPUT2 CALL CHPUT
 PUSH AF
 LD A,(PRINTER)
 OR A
 JR NZ,CHPUT21
 POP AF
 RET 
CHPUT21 POP AF
 CALL LPTOUT
 RET NC
 XOR A
 LD (PRINTER),A
 LD HL,ERRMS1
 CALL MESAGE
 JP MAINLP
;
CHGET1 JP CHGET
;
INPUT LD C,0
INPUT0 CALL CHGET
 LD (HL),A
 CP 7FH
 JR NC,INPUT0
 CP ' '
 JR NC,INPUT1
 CP 3
 JR Z,INPUT3
 CP 13
 JR NZ,INPUT2
INPUT3 PUSH AF
 LD A,13
 CALL CHPUT1
 POP AF
 RET 
INPUT2 CP 8
 JR NZ,INPUT0
 INC C
 DEC C
 JR Z,INPUT0
 LD A,7FH
 CALL CHPUT1
 DEC HL
 DEC C
 INC B
 JR INPUT0
INPUT1 INC B
 DEC B
 JR Z,INPUT0
 CALL CHPUT1
 DEC B
 INC C
 INC HL
 JR INPUT0
;
; HL=PTR, DE=TABEL
; RET A=CODE
;
FDWORD LD B,0
FDWRD1 INC B
 PUSH HL
 LD A,(DE)
 AND 7FH
 JR Z,FDWRD6
 CP (HL)
 JR NZ,FDWRD2
FDWRD3 INC HL
FDWRD7 INC DE
 LD A,(DE)
 OR A
 JR Z,FDWRD8
 CP (HL)
 JR NZ,FDWRD8
 CP ' '
 JR NZ,FDWRD3
 CALL NXTCHR
 JR FDWRD7
FDWRD8 CP 80H
 JR NC,FDWRD4
 OR A
 JR Z,FDWRD4
FDWRD2 INC DE
 LD A,(DE)
 OR A
 JR Z,FDWRD5
 CP 80H
 JR C,FDWRD2
FDWRD5 POP HL
 JR FDWRD1
FDWRD6 XOR A
 POP HL
 RET 
FDWRD4 LD A,(HL)
 CALL TYPCHR
 JR NC,FDWRD5
 POP DE
 LD A,B
 OR A
 RET 
;
NXTCHI INC HL
NXTCHR LD A,(HL)
 CP ' '
 JR Z,NXTCHI
 RET 
;
; TYPE CHAR : C=NOT ALFA & NOT NUM
;             Z=NUM,NZ=ALFA
;
TYPCHR CP '0'
 RET C
 CP 5BH
 JR C,TYPCH1
TYPCH2 SCF 
 RET 
TYPCH1 CP 3AH
 JR NC,TYPCH3
 CP A
 RET 
TYPCH3 CP 'A'
 RET C
 CP 40H
 RET 
;
HEXCHR CALL TYPCHR
 RET C
 RET Z
 CP 'G'
 JR NC,TYPCH2
 CP 40H
 RET 
;
SKPLBL LD DE,LBLBUF
 LD B,8
 LD A,' '
SKPLB2 LD (DE),A
 INC DE
 DJNZ SKPLB2
 LD B,8
 LD DE,LBLBUF
 LD A,(HL)
 CALL TYPCHR
 JP C,RETZRO
 RET Z
 PUSH HL
SKPLB1 LD (DE),A
 INC DE
 INC HL
 LD A,(HL)
 CALL TYPCHR
 JR C,SKPLB3
 DJNZ SKPLB1
 POP HL
 JP RETZRO
SKPLB3 POP DE
 PUSH HL
 EX DE,HL
 LD DE,ILLBL
 CALL FDWORD
 POP HL
 JP NZ,RETZRO
 INC A
 RET 
;
ILLBL DEFW 0C2C1H
 DEFW 0C4C5H
 DEFW 0C8CCH
 DEFW 0D2C9H
 DEFB 0C9H
 DEFM 'XH'
 DEFB 0C9H
 DEFM 'XL'
 DEFB 0C9H
 DEFM 'YH'
 DEFB 0C9H
 DEFM 'YL'
 DEFB 0C2H
 DEFB 'C'
 DEFB 0C4H
 DEFB 'E'
 DEFB 0C8H
 DEFB 'L'
 DEFB 0C1H
 DEFB 'F'
 DEFB 0D3H
 DEFB 'P'
 DEFB 0C9H
 DEFB 'X'
 DEFB 0C9H
 DEFB 'Y'
FLAGS0 DEFB 0CEH
 DEFB 'Z'
 DEFB 0DAH
 DEFB 0CEH
 DEFB 'C'
 DEFB 0C3H
 DEFB 0D0H
 DEFB 'O'
 DEFB 0D0H
 DEFB 'E'
 DEFB 0D0H
 DEFB 0CDH
 NOP 
;
ASMLINI LD A,0FFH
 LD (IMEDIATE),A
 LD HL,0
 LD (LBLPT1),HL
 LD HL,INPBUF
 JR ASMLN1
;
ASMLIN LD HL,INPBUF
ASMLN7 XOR A
 LD (IMEDIATE),A
 PUSH HL
 LD HL,(LBLMAX)
 LD BC,(LBLCNT)
 CP A
 SBC HL,BC
 JR C,ASMLN9
 LD HL,(HIMEM)
 LD BC,(ENDTXT)
 SBC HL,BC
 JR NC,ASMLN8
ASMLN9 CALL MEMFULER
 JP MAINLP
ASMLN8 POP HL
 LD BC,0
 LD (LBLPT1),BC
 LD A,1
 LD (ERROR),A
 LD A,(HL)
 CP ' '
 JR Z,ASMLN1
 CP ';'
 JR NZ,ASMLN6
 LD BC,6EDH
 LD (ASMBUF),BC
 LD A,2
 LD (ASMCNT),A
 JR ASMLN4
ASMLN6 CALL NXTCHR
 LD (LBLPT1),HL
 CALL SKPLBL
 JR Z,ASMLINER
 LD A,(HL)
 CP ' '
 JR NZ,ASMLINER
ASMLN1 LD A,2
 LD (ERROR),A
 CALL NXTCHR
 LD DE,OPCODE
 CALL FDWORD
 JR Z,ASMLINER
 PUSH HL
 LD L,A
 LD H,0
 LD BC,OPCADR-2
 ADD HL,HL
 ADD HL,BC
 LD E,(HL)
 INC HL
 LD D,(HL)
 POP HL
 LD BC,ASMLNR
 PUSH BC
 PUSH DE
 LD B,A
 LD A,3
 LD (ERROR),A
 CALL NXTCHR
 LD A,B
 RET 
ASMLNR JR Z,ASMLINER
 CALL NXTCHR
 CP ';'
 JR NZ,ASMLN2
ASMLN4 INC HL
 LD A,(HL)
 CP ' '
 JR C,ASMLN3
 LD B,A
 CALL INBUF
 JR ASMLN4
ASMLN2 CP ' '
 JR NC,ASMLINER
ASMLN3 LD A,(IMEDIATE)
 OR A
 RET NZ
 LD HL,(LBLPT1)
 LD A,H
 OR L
 JR NZ,ASMLN5
 INC A
 RET 
ASMLN5 LD HL,ASMBUF+125
 LD DE,ASMBUF+127
 LD BC,126
 LDDR 
 LD HL,(LBLPT1)
 CALL ADDLBL
 LD (ASMBUF),BC
 LD A,(ASMCNT)
 INC A
 INC A
 OR 80H
 LD (ASMCNT),A
 RET 
;
ASMLINER LD A,7
 CALL CHPUT1
 CALL CLRBUF
 LD A,(ERROR)
 OR A
 RET Z
 DEC A
 LD DE,ERRTBL
 CALL DISTB
 CALL DISBUF
 LD A,13
 CALL CHPUT1
;
RETZRO XOR A
 RET 
;
ERRTBL DEFB 0AAH
 DEFM '** Bad label'
 DEFB 0AAH
 DEFM '** Bad opcode'
 DEFB 0AAH
 DEFM '** Bad operand'
 DEFB 0AAH
 DEFM '** Expression too long'
 DEFB 0AAH
 DEFM '** No label declaration allowed'
 DEFB 0AAH
 DEFM '** Missing label declaration'
 DEFB 0AAH
 DEFM '** Bad nesting'
 DEFB 80H
;
MEMFULER LD HL,MEMFULMS
 JP MESAGE
;
MEMFULMS DEFB 0D07H
 DEFM '*** Memory full or too many labels'
 DEFW 0DH
;
;
PROCES LD A,(DE)
 OR A
 RET Z
 PUSH HL
 PUSH DE
 XOR A
 LD (EXPCNT),A
 INC A
 LD (VARCNT),A
PROCS1 INC DE
 LD A,(DE)
 CP 128
 JR NC,PROCS2
 CP (HL)
 INC HL
 JR Z,PROCS1
PROCSE POP HL
 LD E,(HL)
 LD D,0
 INC HL
 ADD HL,DE
 EX DE,HL
 POP HL
 JR PROCES
PROCS2 INC A
 JR NZ,PROCS3
 LD A,(HL)
 CP ' '+1
 JR C,PROCS4
 CP ';'
 JR Z,PROCS4
 JR PROCSE
PROCS3 SUB 81H
 PUSH DE
 LD BC,PROCSR
 PUSH BC
 PUSH HL
 LD L,A
 LD H,0
 ADD HL,HL
 LD BC,SETADR
 ADD HL,BC
 LD C,(HL)
 INC HL
 LD B,(HL)
 POP HL
 PUSH BC
 RET 
PROCSR POP DE
 JR NZ,PROCS1
 JR PROCSE
PROCS4 POP BC
 POP BC
 PUSH HL
 XOR A
 LD (ASMCNT),A
PROCS6 INC DE
 LD A,(DE)
 LD B,A
PROCS5 INC DE
 LD A,(DE)
 PUSH DE
 PUSH AF
 BIT 3,A
 JR NZ,PROCS9
 LD C,A
 RRA 
 RRA 
 RRA 
 RRA 
 AND 3
 LD L,A
 LD H,0
 LD DE,VARBUF
 ADD HL,DE
 LD A,C
 AND 7
 LD C,A
 LD A,(HL)
 JR Z,PROCS7
PROCS8 ADD A,A
 DEC C
 JR NZ,PROCS8
PROCS7 OR B
 LD B,A
PROCSQ POP AF
 BIT 7,A
 JR Z,PRCSQ1
 CALL INBUF
 OR A
 POP DE
 POP HL
 RET 
PRCSQ1 POP DE
 BIT 6,A
 JR Z,PROCS5
 CALL INBUF
 JR PROCS6
PROCS9 BIT 5,A
 JR Z,PROCSA
PROCSA BIT 4,A
 JR Z,PROCSB
; !! NOG NIET HELEMAAL OK
 JR PROCSQ
PROCSB PUSH AF
 CALL INBUF
 POP AF
 AND 1
 LD HL,EXPBUF
 JR Z,PROCSC
 LD BC,MXLENEXP+4
 ADD HL,BC
PROCSC LD B,(HL)
 INC HL
 LD A,B
 AND 0D0H
 CP 0D0H
 JR Z,PROCSH
 LD C,B
 CP 90H
 JR Z,PROCSG
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 CP 0C0H
 JR NZ,PROCSD
 PUSH BC
 PUSH HL
 EX DE,HL
 CALL ADDLBL
 LD E,C
 LD D,B
GENERR2 POP HL
 POP BC
PROCSD LD A,D
 OR A
 JR NZ,PROCSF
 SET 3,C
PROCSF LD B,C
 CALL INBUF
 LD B,E
 BIT 3,C
 JR NZ,PROCSG
 CALL INBUF
 LD B,D
PROCSG LD A,C
 AND 7
 JR Z,PROCSQ
PROCSH CALL INBUF
 JR PROCSC
;
EVLEXP CALL EVLXP3
 RET Z
 LD A,(BRACKETS)
 OR A
 JP NZ,RETZRO
 DEC A
 RET 
;
EVLXP3 EX DE,HL
 XOR A
 LD (LENEXP),A
 LD (NESTING),A
 LD HL,EXPBUF
 LD A,(EXPCNT)
 INC A
 LD (EXPCNT),A
 DEC A
 AND 1
 JR Z,EVLXP1
 LD BC,MXLENEXP+4
 ADD HL,BC
EVLXP1 EX DE,HL
 XOR A
 LD (BRACKETS),A
 LD (STOPEXP),A
EVLXP2 LD A,(LENEXP)
 CP MXLENEXP
 JR NC,EXPTLNER
 CALL EVLTRM
 RET Z
 LD A,(LENEXP)
 CP MXLENEXP
 JR NC,EXPTLNER
 LD A,(BRACKETS)
 OR A
 RET NZ
 LD A,(STOPEXP)
 OR A
 RET NZ
 JR EVLXP2
;
EXPTLNER LD A,4
 LD (ERROR),A
 XOR A
 RET 
;
EVLTRM LD A,(HL)
 CP '-'
 LD B,0
 JR NZ,EVLTR5
 INC HL
 LD B,20H
EVLTR5 LD A,(HL)
 CP '('
 JR NZ,EVLTR7
 LD A,(NESTING)
 INC A
 LD (NESTING),A
 CP MAXNEST
 JR NZ,EVLTR8
 LD A,7
 LD (ERROR),A
 XOR A
 RET 
EVLTR8 LD A,0D0H
 OR B
 LD (DE),A
 LD A,(LENEXP)
 INC A
 LD (LENEXP),A
 INC DE
 INC HL
 CALL EVLXP2
 RET Z
 LD A,(NESTING)
 DEC A
 LD (NESTING),A
 LD A,(BRACKETS)
 OR A
 RET Z
 DEC A
 LD (BRACKETS),A
 LD A,(STOPEXP)
 OR A
 RET NZ
 LD A,(LENEXP)
 INC A
 LD (LENEXP),A
 LD A,0D1H
 LD (DE),A
 INC DE
 OR A
 RET 
EVLTR7 PUSH DE
EVLTR1 PUSH BC
 LD A,(HL)
 CP '$'
 JR NZ,EVLTR9
 INC HL
 LD A,90H
 JR EVLTRQ
EVLTR9 CP 27H
 JR NZ,EVLTR2
 INC HL
 LD E,(HL)
 INC HL
 LD D,0
 LD A,(HL)
 INC HL
 CP 27H
 LD A,80H
 JR Z,EVLTRQ
EVLTRE POP BC
 POP DE
 CP A
 RET 
EVLTR2 CP 'A'
 JR C,EVLTR3
 LD A,(IMEDIATE)
 OR A
 JR NZ,EVLTRE
 PUSH HL
 CALL SKPLBL
 POP DE
 JR Z,EVLTRE
 LD A,0C0H
 JR EVLTRQ
EVLTR3 CALL EVLCNS
 JR Z,EVLTRE
EVLTRQ POP BC
 OR B
 LD C,E
 LD B,D
 LD E,A
 LD A,(BRACKETS)
 LD D,A
EVLTR6 LD A,(HL)
 INC HL
 INC D
 CP ')'
 JR Z,EVLTR6
 DEC HL
 DEC D
 LD A,D
 LD (BRACKETS),A
 LD D,6
 LD A,(HL)
 CP '!'
 JR Z,EVLTR4
 DEC D
 CP '&'
 JR Z,EVLTR4
 DEC D
 CP '*'
 JR Z,EVLTR4
 DEC D
 CP '/'
 JR Z,EVLTR4
 DEC D
 CP '-'
 JR Z,EVLTR4
 DEC D
 CP '+'
 JR Z,EVLTR4
 DEC D
 DEC HL
EVLTR4 LD A,E
 OR D
 INC HL
 POP DE
 LD (DE),A
 INC DE
 AND 0DFH
 CP 90H
 JR C,EVLTRA
 CP 0D0H
 JR NC,EVLTRC
 CP 0C0H
 JR NC,EVLTRA
EVLTRC LD C,1
 JR EVLTRB
EVLTRA EX DE,HL
 LD (HL),C
 INC HL
 LD (HL),B
 INC HL
 EX DE,HL
 LD C,3
EVLTRB LD B,A
 LD A,(LENEXP)
 ADD A,C
 LD (LENEXP),A
 LD A,B
 AND 7
 RET NZ
 DEC A
 LD (STOPEXP),A
 LD A,0
 RET 
;
EVLCNS LD A,(HL)
 CALL HEXCHR
 JP C,RETZRO
 PUSH HL
EVLCS1 INC HL
 LD A,(HL)
 CALL HEXCHR
 JR NC,EVLCS1
 EX DE,HL
 LD HL,0
 CP 'H'
 JR Z,EVLCS9
 CP 'O'
 JR NZ,EVLCS6
EVLCS9 POP DE
 CP 'H'
 JR Z,EVLCSH
 CP 'O'
 JP NZ,RETZRO
EVLCSO LD A,(DE)
 INC DE
 CP '8'
 JR NC,EVLCS7
 SUB '0'
 LD C,A
 LD B,0
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,BC
 JR EVLCSO
EVLCS7 LD A,10H
 JR EVLCSQ
EVLCSH LD A,(DE)
 INC DE
 CALL HEXCHR
 JR C,EVLCS3
 JR Z,EVLCS4
 SUB 7
EVLCS4 SUB '0'
 LD C,A
 LD B,0
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,HL
 ADD HL,BC
 JR EVLCSH
EVLCS3 LD A,40H
 JR EVLCSQ
EVLCS6 DEC DE
 LD A,(DE)
 POP DE
 CP 'B'
 JR NZ,EVLCSD
EVLCSB LD A,(DE)
 INC DE
 CP '2'
 JR NC,EVLCS8
 SUB '0'
 LD C,A
 LD B,0
 ADD HL,HL
 ADD HL,BC
 JR EVLCSB
EVLCS8 LD A,50H
 JR EVLCSQ
EVLCSD LD A,(DE)
 CALL TYPCHR
 JR C,EVLCS5
 JR NZ,EVLCS5
 SUB '0'
 LD C,L
 LD B,H
 ADD HL,HL
 ADD HL,HL
 ADD HL,BC
 ADD HL,HL
 LD C,A
 LD B,0
 ADD HL,BC
 INC DE
 JR EVLCSD
EVLCS5 XOR A
EVLCSQ EX DE,HL
 LD B,A
 INC B
 RET 
;
SETADR DEFW RCREG0
 DEFW RCREG1
 DEFW RCREG2
 DEFW RCREG3
 DEFW RCREG4
 DEFW RCREG5
 DEFW RCREG6
 DEFW RCREG7
 DEFW RCREG8
 DEFW RCREG9
 DEFW RCREGA
 DEFW RCREGB
 DEFW RCREGC
 DEFW RCREGD
 DEFW RCREGE
 DEFW RCREGF
;
RCREG0 LD DE,REG0
 CALL FDWORD
 RET Z
 CP 7
 RET Z
;
VRIMEM PUSH HL
 DEC A
 LD DE,(VARCNT)
 LD D,0
 LD HL,VARBUF
 ADD HL,DE
 LD (HL),A
 LD A,E
 INC A
 LD (VARCNT),A
 POP HL
 RET 
;
RCREG1 LD DE,REG1
RCRG11 CALL FDWORD
 RET Z
 JR VRIMEM
;
RCREG2 LD DE,REG2
 JR RCRG11
;
RCREG3 LD DE,REG3
 CALL FDWORD
 RET Z
 CP 2
 RET Z
 JR VRIMEM
;
RCREG4 LD DE,REG4
 JR RCRG11
;
RCREG5 LD DE,REG0
 JR RCRG11
;
RCREG6 LD A,(HL)
 CP '('
 JP NZ,RETZRO
 INC HL
 LD DE,REG8
 CALL FDWORD
 RET Z
 CALL VRIMEM
 LD A,(HL)
 CP '+'
 JR Z,RCRG61
 CP '-'
 JR Z,RCRG62
 PUSH HL
 LD HL,ZERO
 CALL EVLEXP
 POP HL
 LD A,(HL)
 CP ')'
 JP NZ,RETZRO
 OR A
 INC HL
 RET 
RCRG61 INC HL
RCRG62 CALL EVLXP3
 RET Z
 LD A,(BRACKETS)
 DEC A
 JP NZ,RETZRO
 DEC A
 RET 
;
ZERO DEFW 30H
;
RCREG7 LD A,(HL)
 CP '('
 JP NZ,RETZRO
 JR RCRG61
;
RCREG8 LD DE,REG8
 JR RCRG11
;
RCREG9 LD DE,REG9
 CALL FDWORD
 RET Z
 CP 5
 RET Z
 JP VRIMEM
;
RCREGA LD DE,REG9
 CALL FDWORD
 RET Z
 CP 4
 RET Z
 JP C,VRIMEM
 DEC A
 JP VRIMEM
;
RCREGB LD DE,REG9
 CALL FDWORD
 RET Z
 CP 3
 RET Z
 CP 5
 RET Z
 JP VRIMEM
;
RCREGC LD DE,FLAGS0
 CALL FDWORD
 RET Z
 CP 5
 JP NC,RETZRO
 JP VRIMEM
;
RCREGD LD DE,FLAGS0
 JP RCRG11
;
RCREGE LD A,(HL)
 SUB '0'
 JP C,RETZRO
 CP 8
 JP NC,RETZRO
 INC A
 INC HL
 JP VRIMEM
;
RCREGF JP EVLEXP
;
REG0 DEFW 0C3C2H
 DEFW 0C5C4H
 DEFW 0CCC8H
REG3 DEFB 0A8H
 DEFM 'HL)'
 DEFW 0C1H
;
REG1 DEFW 0C3C2H
 DEFW 0C5C4H
 DEFB 0C9H
 DEFM 'XH'
 DEFB 0C9H
 DEFM 'XL'
 DEFB 81H
 DEFW 0C1H
;
REG2 DEFW 0C3C2H
 DEFW 0C5C4H
 DEFB 0C9H
 DEFM 'YH'
 DEFB 0C9H
 DEFM 'YL'
 DEFB 81H
 DEFW 0C1H
;
REG4 DEFB 0A8H
 DEFM 'BC)'
 DEFB 0A8H
 DEFM 'DE)'
 NOP 
;
REG8 DEFB 0C9H
 DEFM 'X'
 DEFB 0C9H
 DEFM 'Y'
 NOP 
;
REG9 DEFB 0C2H
 DEFB 'C'
 DEFB 0C4H
 DEFB 'E'
 DEFB 0C8H
 DEFB 'L'
 DEFB 0D3H
 DEFB 'P'
 DEFB 0C1H
 DEFB 'F'
 NOP 
;
INBUF PUSH HL
 PUSH DE
 LD HL,(ASMCNT)
 LD H,0
 LD DE,ASMBUF
 LD A,L
 ADD HL,DE
 LD (HL),B
 INC A
 LD (ASMCNT),A
 POP DE
 POP HL
 RET 
;
OPCODE DEFB 0CCH;0
 DEFB 'D'
 DEFB 0D0H
 DEFM 'OP'
 DEFB 0D0H
 DEFM 'USH'
 DEFB 0C5H;3
 DEFM 'X'
 DEFB 0C5H
 DEFM 'XX'
 DEFB 0CCH
 DEFM 'DI'
 DEFB 0CCH
 DEFM 'DD'
 DEFB 0CCH;7
 DEFM 'DIR'
 DEFB 0CCH
 DEFM 'DDR'
;
 DEFB 0C3H
 DEFM 'PI'
 DEFB 0C3H
 DEFM 'PD'
 DEFB 0C3H;11
 DEFM 'PIR'
 DEFB 0C3H
 DEFM 'PDR'
;
 DEFB 0C1H
 DEFM 'DD'
 DEFB 0C1H
 DEFM 'DC'
 DEFB 0D3H;15
 DEFM 'UB'
 DEFB 0D3H
 DEFM 'BC'
 DEFB 0C1H
 DEFM 'ND'
 DEFB 0D8H
 DEFM 'OR'
 DEFB 0CFH;19
 DEFM 'R'
 DEFB 0C3H
 DEFM 'P'
;
 DEFB 0C9H
 DEFM 'NC'
 DEFB 0C4H
 DEFM 'EC'
;
 DEFB 0C4H;23
 DEFM 'AA'
 DEFB 0C3H
 DEFM 'PL'
 DEFB 0D3H
 DEFM 'CF'
 DEFB 0C3H
 DEFM 'CF'
;
 DEFB 0CEH;27
 DEFM 'EG'
 DEFB 0CEH
 DEFM 'OP'
 DEFB 0C8H
 DEFM 'ALT'
 DEFB 0C4H
 DEFB 'I'
 DEFB 0C5H;31
 DEFB 'I'
;
 DEFB 0C9H
 DEFM 'M 0'
 DEFB 0C9H
 DEFM 'M 1'
 DEFB 0C9H
 DEFM 'M 2'
;
 DEFB 0D2H;35
 DEFM 'LCA'
 DEFB 0D2H
 DEFM 'RCA'
 DEFB 0D2H
 DEFM 'LA'
 DEFB 0D2H
 DEFM 'RA'
;
 DEFB 0D2H;39
 DEFM 'LC'
 DEFB 0D2H
 DEFM 'RC'
 DEFB 0D2H
 DEFB 'L'
 DEFB 0D2H
 DEFB 'R'
 DEFB 0D3H;43
 DEFM 'LA'
 DEFB 0D3H
 DEFM 'RA'
; EXTRA
 DEFB 0D3H
 DEFM 'LL'
;
 DEFB 0D3H
 DEFM 'RL'
;
 DEFB 0D2H;47
 DEFM 'LD'
 DEFB 0D2H
 DEFM 'RD'
;
 DEFB 0C2H
 DEFM 'IT'
 DEFB 0D2H
 DEFM 'ES'
 DEFB 0D3H;51
 DEFM 'ET'
;
 DEFB 0CAH
 DEFB 'P'
 DEFB 0CAH
 DEFB 'R'
 DEFB 0C4H
 DEFM 'JNZ'
 DEFB 0C3H;55
 DEFM 'ALL'
 DEFB 0D2H
 DEFM 'ET'
 DEFB 0D2H
 DEFM 'ETI'
 DEFB 0D2H
 DEFM 'ETN'
;
 DEFB 0D2H;59
 DEFM 'ST'
 DEFB 0C9H
 DEFB 'N'
 DEFB 0CFH
 DEFM 'UT'
;
 DEFB 0C9H
 DEFM 'NI'
 DEFB 0C9H;63
 DEFM 'ND'
 DEFB 0C9H
 DEFM 'NIR'
 DEFB 0C9H
 DEFM 'NDR'
;
 DEFB 0CFH
 DEFM 'UTI'
 DEFB 0CFH;67
 DEFM 'UTD'
 DEFB 0CFH
 DEFM 'TIR'
 DEFB 0CFH
 DEFM 'TDR'
;
 DEFB 0C4H
 DEFM 'EFB'
 DEFB 0C4H;71
 DEFM 'EFW'
 DEFB 0C4H
 DEFM 'EFM'
 DEFB 0C4H
 DEFM 'EFS'
;
 DEFB 0CFH
 DEFM 'RG'
 DEFB 0C5H
 DEFM 'QU'
;
 DEFB 81H; DUMMY
;
 DEFB 0C5H
 DEFM 'ND'
;
 NOP 
;
OPCADR DEFW CMLD
 DEFW CMPSPP
 DEFW CMPSPP
 DEFW CMEX
 DEFW CMEXX
 DEFW CMLDR
 DEFW CMLDR
 DEFW CMLDR
 DEFW CMLDR
 DEFW CMCPR
 DEFW CMCPR
 DEFW CMCPR
 DEFW CMCPR
 DEFW CMARIT
 DEFW CMARIT
 DEFW CMART8
 DEFW CMARIT
 DEFW CMART8
 DEFW CMART8
 DEFW CMART8
 DEFW CMART8
 DEFW CMICDC
 DEFW CMICDC
 DEFW CMGPO
 DEFW CMGPO
 DEFW CMGPO
 DEFW CMGPO
 DEFW CMNEG
 DEFW CMNOP
 DEFW CMHALT
 DEFW CMDI
 DEFW CMEI
 DEFW CMIM0
 DEFW CMIM1
 DEFW CMIM2
 DEFW CMRA
 DEFW CMRA
 DEFW CMRA
 DEFW CMRA
;
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
 DEFW CMRTSH
;
 DEFW CMRLD
 DEFW CMRRD
;
 DEFW CMBIT
 DEFW CMBIT
 DEFW CMBIT
;
 DEFW CMJP
 DEFW CMJR
 DEFW CMDJNZ
 DEFW CMCALL
 DEFW CMRET
 DEFW CMRETI
 DEFW CMRETN
;
 DEFW CMRST
 DEFW CMIN
 DEFW CMOUT
;
 DEFW CMINR
 DEFW CMINR
 DEFW CMINR
 DEFW CMINR
 DEFW CMOTR
 DEFW CMOTR
 DEFW CMOTR
 DEFW CMOTR
;
 DEFW PSD0
 DEFW PSD0
 DEFW PSD2
 DEFW PSD0
;
 DEFW PSD4
 DEFW PSD5
;
 DEFW 0; KAN NIET VOORKOMEN
 DEFW PSD6
;
CMLD LD DE,OPLD
 JP PROCES
;
CMPSPP SUB 2
 LD (VARBUF),A
 LD DE,OPPSPP
 JP PROCES
;
CMEX LD DE,OPEX
 JP PROCES
;
CMEXX LD A,1
 LD (ASMCNT),A
 LD A,0D9H
 LD (ASMBUF),A
 OR A
 RET 
;
CMLDR LD B,0A0H
 SUB 6
CMLDR1 ADD A,A
 ADD A,A
 ADD A,A
 OR B
 LD (ASMBUF+1),A
 LD A,0EDH
 LD (ASMBUF),A
 LD A,2
 LD (ASMCNT),A
 RET 
;
CMCPR LD B,0A1H
 SUB 10
 JR CMLDR1
CMARIT LD B,A
 LD A,(HL)
 CP 'A'
 JR Z,CMART1
 LD A,B
 CP 14
 LD DE,OPAD16
 JP Z,PROCES
 LD A,17
 SUB B
 SRL A
 LD (VARBUF),A
 LD DE,OPAS16
 JP PROCES
CMART1 INC HL
 LD A,(HL)
 INC HL
 CP ','
 JP NZ,RETZRO
 LD A,B
;
CMART8 SUB 14
 LD (VARBUF),A
 LD DE,OPART8
 JP PROCES
;
CMICDC SUB 22
 LD (VARBUF),A
 LD DE,OPICDC
 JP PROCES
;
CMGPO SUB 24
 ADD A,A
 ADD A,A
 ADD A,A
 OR 27H
CMGPO1 LD (ASMBUF),A
 LD A,1
 LD (ASMCNT),A
 OR A
 RET 
;
CMNEG LD DE,44EDH
CMNEG1 LD (ASMBUF),DE
 LD A,2
 LD (ASMCNT),A
 OR A
 RET 
;
CMNOP XOR A
 JR CMGPO1
;
CMHALT LD A,76H
 JR CMGPO1
;
CMDI LD A,0F3H
 JR CMGPO1
;
CMEI LD A,0FBH
 JR CMGPO1
;
CMIM0 LD DE,46EDH
 JR CMNEG1
;
CMIM1 LD DE,56EDH
 JR CMNEG1
;
CMIM2 LD DE,5EEDH
 JR CMNEG1
;
CMRA SUB 36
 ADD A,A
 ADD A,A
 ADD A,A
 OR 7
 JR CMGPO1
;
CMRTSH SUB 40
 LD (VARBUF),A
 LD DE,OPRTSH
 JP PROCES
;
CMRLD LD DE,6FEDH
 JR CMNEG1
;
CMRRD LD DE,67EDH
 JP CMNEG1
;
CMBIT SUB 49
 LD (VARBUF),A
 LD DE,OPBIT
 JP PROCES
;
CMJP LD DE,OPJP
 JP PROCES
;
CMJR LD DE,OPJR
 JP PROCES
;
CMDJNZ LD DE,OPDJNZ
 JP PROCES
;
CMCALL LD DE,OPCALL
 JP PROCES
;
CMRET LD DE,OPRET
 JP PROCES
;
CMRETI LD DE,4DEDH
 JP CMNEG1
;
CMRETN LD DE,45EDH
 JP CMNEG1
;
CMRST LD DE,OPRST
 JP PROCES
;
CMIN LD DE,OPIN
 JP PROCES
;
CMOUT LD DE,OPOUT
 JP PROCES
;
CMINR LD B,0A2H
 SUB 63
 JP CMLDR1
;
CMOTR LD B,0A3H
 SUB 67
 JP CMLDR1
;
PSD4 LD B,A
 LD DE,(LBLPT1)
 LD A,D
 OR E
 JR Z,PSD41
 LD A,5
 LD (ERROR),A
 JP RETZRO
PSD41 LD A,B
 JR PSD0
;
PSD5 LD B,A
 LD A,(IMEDIATE)
 OR A
 JR NZ,PSD52
 LD DE,(LBLPT1)
 LD A,D
 OR E
 JR NZ,PSD51
 LD A,6
 LD (ERROR),A
 JP RETZRO
PSD51 LD A,B
;
PSD0 SUB 71
 LD (VARBUF),A
 LD DE,OPPSD0
 JP PROCES
;
PSD52 LD A,2
 LD (ERROR),A
 XOR A
 RET 
;
PSD6 LD DE,7EDH
 LD (ASMBUF),DE
 LD A,2
 LD (ASMCNT),A
 OR A
 RET 
;
OPPSD0 DEFB 7
 DEFW 0FF8FH
 DEFW 58EDH
 DEFW 0
 DEFW 88H
;
PSD2 LD A,0EDH
 LD (ASMBUF),A
 LD A,2
 LD (ASMBUF+1),A
 INC A
 LD (ASMCNT),A
 LD A,(HL)
 CP 27H
 JP NZ,RETZRO
 LD C,0
PSD21 INC HL
 LD A,(HL)
 CP 27H
 JR Z,PSD22
 CP ' '
 JP C,RETZRO
 INC C
 LD B,A
 CALL INBUF
 JR PSD21
PSD22 LD A,C
 LD (ASMBUF+2),A
 OR A
 INC HL
 RET 
;
OPLD DEFB 7;LD R,S
 DEFW 2C80H
 DEFW 0FF80H
 DEFW 1340H
 DEFB 0A0H
 DEFB 9;LD R,S (IX)
 DEFW 2C81H
 DEFW 0FF81H
 DEFW 58DDH
 DEFW 1340H
 DEFB 0A0H
 DEFB 9;LD R,S (IY)
 DEFW 2C82H
 DEFW 0FF82H
 DEFW 58FDH
 DEFW 1340H
 DEFB 0A0H
 DEFB 6;LD R,(HL)
 DEFW 2C80H
 DEFW 0FF83H
 DEFW 9346H
 DEFB 6;LD (HL),R
 DEFW 2C83H
 DEFW 0FF80H
 DEFW 0A070H
 DEFB 8;LD I,A
 DEFW 2C49H
 DEFW 0FF41H
 DEFW 58EDH
 DEFW 9847H
 DEFB 8;LD R,A
 DEFW 2C52H
 DEFW 0FF41H
 DEFW 58EDH
 DEFW 984FH
 DEFB 8;LD A,I
 DEFW 2C41H
 DEFW 0FF49H
 DEFW 58EDH
 DEFW 9857H
 DEFB 8;LD A,R
 DEFW 2C41H
 DEFW 0FF52H
 DEFW 58EDH
 DEFW 985FH
 DEFB 6;LD A,(BC)/(DE)
 DEFW 2C41H
 DEFW 0FF84H
 DEFW 940AH
 DEFB 6;LD (BC)/(DE),A
 DEFW 2C84H
 DEFW 0FF41H
 DEFW 9402H
 DEFB 9;LD R,(IX+D)
 DEFW 2C80H
 DEFW 0FF86H
 DEFW 65DDH
 DEFW 1346H
 DEFB 88H
 DEFB 9;LD (IX+D),R
 DEFW 2C86H
 DEFW 0FF80H
 DEFW 55DDH
 DEFW 2070H
 DEFB 88H
 DEFB 6;LD A,(NN)
 DEFW 2C41H
 DEFW 0FF87H
 DEFW 883AH
 DEFB 6;LD (NN),A
 DEFW 2C87H
 DEFW 0FF41H
 DEFW 8832H
 DEFB 7;LD R,N
 DEFW 2C85H
 DEFW 0FF8FH
 DEFW 1306H
 DEFB 88H
 DEFB 9;LD R,N (IX)
 DEFW 2C81H
 DEFW 0FF8FH
 DEFW 58DDH
 DEFW 1306H
 DEFB 88H
 DEFB 9;LD R,N (IY)
 DEFW 2C82H
 DEFW 0FF8FH
 DEFW 58FDH
 DEFW 1306H
 DEFB 88H
 DEFB 9;LD (IX+D),N
 DEFW 2C86H
 DEFW 0FF8FH
 DEFW 55DDH
 DEFW 836H
 DEFB 89H
 DEFB 7;LD HL,(NN)
 DEFM 'HL'
 DEFW 872CH
 DEFW 2AFFH
 DEFB 88H
 DEFB 9;LD DD,(NN)
 DEFW 2C89H
 DEFW 0FF87H
 DEFW 58EDH
 DEFW 144BH
 DEFB 88H
 DEFB 8;LD IX,(NN)
 DEFW 2C88H
 DEFW 0FF87H
 DEFW 55DDH
 DEFW 882AH
 DEFB 7;LD DD,NN
 DEFW 2C89H
 DEFW 0FF8FH
 DEFW 1401H
 DEFB 88H
 DEFB 8;LD IX,NN
 DEFW 2C88H
 DEFW 0FF8FH
 DEFW 55DDH
 DEFW 8821H
 DEFB 7;LD (NN),HL
 DEFW 2C87H
 DEFW 4C48H
 DEFW 22FFH
 DEFB 88H
 DEFB 9;LD (NN),DD
 DEFW 2C87H
 DEFW 0FF89H
 DEFW 58EDH
 DEFW 1443H
 DEFB 88H
 DEFB 8;LD (NN),IX
 DEFW 2C87H
 DEFW 0FF88H
 DEFW 55DDH
 DEFW 8822H
 DEFB 8;LD SP,HL
 DEFW 5053H
 DEFW 482CH
 DEFW 0FF4CH
 DEFW 98F9H
 DEFB 9;LD SP,IX
 DEFW 5053H
 DEFW 882CH
 DEFW 0DDFFH
 DEFW 0F955H
 DEFB 98H
 NOP 
;
OPPSPP DEFB 5
 DEFW 0FF8AH
 DEFW 2C1H
 DEFB 94H
 DEFB 6
 DEFW 0FF88H
 DEFW 55DDH
 DEFW 82E1H
 NOP 
;
OPEX DEFB 8
 DEFM 'DE,HL'
 DEFW 0EBFFH
 DEFB 98H
 DEFB 9
 DEFM 'AF,AF'
 DEFB 27H
 DEFW 8FFH
 DEFB 98H
 DEFB 10
 DEFM '(SP),HL'
 DEFW 0E3FFH
 DEFB 98H
 DEFB 11
 DEFM '(SP),'
 DEFW 0FF88H
 DEFW 55DDH
 DEFW 98E3H
 NOP 
;
OPART8 DEFB 5
 DEFW 0FF85H
 DEFW 380H
 DEFB 90H
 DEFB 7
 DEFW 0FF81H
 DEFW 58DDH
 DEFW 380H
 DEFB 90H
 DEFB 7
 DEFW 0FF82H
 DEFW 58FDH
 DEFW 380H
 DEFB 90H
 DEFB 7
 DEFW 0FF86H
 DEFW 55DDH
 DEFW 386H
 DEFB 88H
 DEFB 5
 DEFW 0FF8FH
 DEFW 3C6H
 DEFB 88H
 NOP 
;
OPAD16 DEFB 7
 DEFM 'HL,'
 DEFW 0FF89H
 DEFW 9409H
 DEFB 8
 DEFW 2C88H
 DEFW 0FF8BH
 DEFW 55DDH
 DEFW 0A409H
 DEFB 10
 DEFM 'IX,IX'
 DEFW 0DDFFH
 DEFW 2958H
 DEFB 98H
 DEFB 10
 DEFM 'IY,IY'
 DEFW 0FDFFH
 DEFW 2958H
 DEFB 98H
 NOP 
;
OPAS16 DEFB 10
 DEFM 'HL,'
 DEFW 0FF89H
 DEFW 58EDH
 DEFW 342H
 DEFW 94H
;
OPICDC DEFB 5
 DEFW 0FF85H
 DEFW 4
 DEFB 93H
 DEFB 7
 DEFW 0FF81H
 DEFW 58DDH
 DEFW 4
 DEFB 93H
 DEFB 7
 DEFW 0FF82H
 DEFW 58FDH
 DEFW 4
 DEFB 93H
 DEFB 7
 DEFW 0FF86H
 DEFW 55DDH
 DEFW 34H
 DEFB 88H
 DEFB 5
 DEFW 0FF89H
 DEFW 303H
 DEFB 94H
 DEFB 6
 DEFW 0FF88H
 DEFW 55DDH
 DEFW 8323H
 NOP 
;
OPRTSH DEFB 7
 DEFW 0FF85H
 DEFW 58CBH
 DEFW 300H
 DEFB 90H
 DEFB 8
 DEFW 0FF86H
 DEFW 55DDH
 DEFW 48CBH
 DEFW 8306H
 NOP 
;
OPBIT DEFB 10
 DEFW 2C8EH
 DEFW 0FF85H
 DEFW 58CBH
 DEFW 600H
 DEFW 0A013H
 DEFB 11
 DEFW 2C8EH
 DEFW 0FF86H
 DEFW 65DDH
 DEFW 48CBH
 DEFW 1306H
 DEFB 86H
 NOP 
;
OPJP DEFB 7
 DEFW 2C8DH
 DEFW 0FF8FH
 DEFW 13C2H
 DEFB 88H
 DEFB 4
 DEFW 0FF8FH
 DEFW 88C3H
 DEFB 4
 DEFW 0FF83H
 DEFW 98E9H
 DEFB 8
 DEFB '('
 DEFB 88H
 DEFB ')'
 DEFB 0FFH
 DEFW 55DDH
 DEFW 98E9H
 NOP 
;
OPJR DEFB 7
 DEFW 2C8CH
 DEFW 0FF8FH
 DEFW 1320H
 DEFB 88H
 DEFB 4
 DEFW 0FF8FH
 DEFW 8818H
 NOP 
;
OPDJNZ DEFB 4
 DEFW 0FF8FH
 DEFW 8810H
 NOP 
;
OPCALL DEFB 7
 DEFW 2C8DH
 DEFW 0FF8FH
 DEFW 13C4H
 DEFB 88H
 DEFB 4
 DEFW 0FF8FH
 DEFW 88CDH
 NOP 
;
OPRET DEFB 4
 DEFW 0FF8DH
 DEFW 93C0H
 DEFB 3
 DEFW 0C9FFH
 DEFB 98H
 NOP 
;
OPRST DEFB 4
 DEFW 0FF8FH
 DEFW 88C7H
 NOP 
;
OPIN DEFB 6
 DEFM 'A,'
 DEFW 0FF87H
 DEFW 88DBH
 DEFB 10
 DEFB 80H
 DEFM ',(C)'
 DEFW 0EDFFH
 DEFW 4058H
 DEFB 93H
 DEFB 6
 DEFM 'A,'
 DEFW 0FF8FH
 DEFW 88DBH
 NOP 
;
OPOUT DEFB 6
 DEFB 87H
 DEFM ',A'
 DEFW 0D3FFH
 DEFB 88H
 DEFB 6
 DEFB 8FH
 DEFM ',A'
 DEFW 0D3FFH
 DEFB 88H
 DEFB 10
 DEFM '(C),'
 DEFB 80H
 DEFW 0EDFFH
 DEFW 4158H
 DEFB 93H
 NOP 
;
;
;
;
SKPEXP LD A,(DISMOD)
 INC A
 INC HL
 RET Z
 DEC HL
SKPXP1 LD A,(HL)
 AND 0D0H
 CP 90H
 LD A,(HL)
 JR Z,SKPXP2
 INC HL
 AND 0DFH
 CP 0D0H
 JR NC,SKPXP1
SKPXP3 BIT 3,A
 JR NZ,SKPXP2
 INC HL
SKPXP2 INC HL
 AND 7
 JR NZ,SKPXP1
 RET 
;
DISFLG LD DE,FLAGS0
 JR DSRGH1
;
DISHL LD A,20H
;
DISRG9 LD DE,REG9
 AND 30H
 JR DSRGA1
;
DISRGA LD DE,REG9
 AND 30H
 CP 30H
 JR NZ,DSRGA1
 ADD A,10H
DSRGA1 CP 20H
 JR NZ,DSREGH
 LD A,(INDEX)
 OR A
 JR NZ,DSRGA3
 LD A,20H
 JR DSREGH
DSRGA3 LD DE,REG8
 CP 0DDH
 LD A,0
 JP Z,DISTB
 INC A
 JP DISTB
;
DSREGH RRA 
DSRGH1 RRA 
 RRA 
 RRA 
 AND 7
 JP DISTB
;
IXYHLT DEFB 0C9H
 DEFM 'XH'
 DEFB 0C9H
 DEFM 'XL'
 DEFB 0C9H
 DEFM 'YH'
 DEFB 0C9H
 DEFM 'YL'
 DEFB 128
;
DISRG5 RRA 
 RRA 
 RRA 
 AND 7
 CP 7
 JR Z,DSRG51
 CP 4
 JR C,DSRG51
 LD D,A
 LD A,(INDEX)
 OR A
 JR Z,DSRG52
 LD A,D
 CP 6
 JR Z,DSRG53
 LD A,(INDEXT)
 DEC A
 JR NZ,DSRG52
 LD A,(INDEX)
 LD E,0
 CP 0DDH
 JR Z,DSRG54
 LD E,2
DSRG54 LD A,D
 SUB 4
 ADD A,E
 LD DE,IXYHLT
 JR DISTB
DSRG53 LD A,'('
 CALL PUTBUF
 LD A,20H
 CALL DISRG9
 INC HL
 LD A,(DISMOD)
 INC A
 JR NZ,DSRG57
 LD A,(HL)
 LD D,'+'
 CP 80H
 JR C,DSRG56
 NEG 
 LD D,'-'
DSRG56 LD E,A
 LD A,D
 CALL PUTBUF
 LD D,0
 PUSH HL
 CALL DISHEX
 POP HL
 JR DSRG58
DSRG57 BIT 5,(HL)
 JR NZ,DSRG55
 LD A,'+'
 CALL PUTBUF
DSRG55 PUSH BC
 CALL DISEXP
 POP BC
DSRG58 LD A,')'
 JP PUTBUF
;
DSRG52 LD A,D
DSRG51 LD DE,REG0
;
DISTB PUSH HL
 PUSH BC
 EX DE,HL
 CALL DISTB1
 POP BC
 POP HL
 RET 
;
DISLD XOR A
;
DISOPC PUSH HL
 PUSH BC
 LD HL,OPCODE
 CALL DISTB1
DISOP1 LD A,' '
 CALL PUTBUF
 DJNZ DISOP1
 POP BC
 POP HL
 RET 
;
DISTBLIM PUSH BC
 CALL CLRBUF
 CALL DISTB1
 CALL DISBUF
 POP BC
 RET 
;
DISTB1 OR A
 JR Z,DISOP3
 LD B,A
 INC B
DISOP4 LD A,(HL)
 INC HL
 CP 80H
 JR C,DISOP4
 DJNZ DISOP4
 DEC HL
DISOP3 LD B,4
 LD A,(HL)
 AND 7FH
 CALL PUTBUF
DISOP2 INC HL
 LD A,(HL)
 CP 80H
 JR NC,DISOP5
 OR A
 JR Z,DISOP5
 CALL PUTBUF
 DEC B
 JR DISOP2
DISOP5 RET 
;
CLRBUF PUSH HL
 LD HL,OUTBUF
 LD (BUFPNT),HL
 LD B,128
CLRBF1 LD (HL),' '
 INC HL
 DJNZ CLRBF1
 POP HL
 RET 
;
PUTBUF PUSH HL
 LD HL,(BUFPNT)
 LD (HL),A
 INC HL
 LD (BUFPNT),HL
 POP HL
 RET 
;
DISBUF LD HL,(BUFPNT)
 LD (HL),0
 LD HL,OUTBUF
;
MESAGE LD A,(HL)
 OR A
 RET Z
 CALL CHPUT1
 INC HL
 JR MESAGE
;
NICEMESG LD HL,(BUFPNT)
 LD C,A
 LD (HL),0
 LD HL,OUTBUF
 JR NICEMES1
NICEMESL LD A,(CSRX)
 CP 2
 JR NC,NICEMES1
 LD A,(PRINTER)
 OR A
 JR Z,NICEMES4
 LD A,13
 CALL LPTOUT
 LD A,10
 CALL LPTOUT
NICEMES4 LD B,C
 LD A,' '
NICEMES2 CALL CHPUT1
 DJNZ NICEMES2
NICEMES1 LD A,(HL)
 INC HL
 CP ' '
 JR C,NICEMES3
 CALL CHPUT1
 JR NICEMESL
NICEMES3 LD A,13
 JP CHPUT1
;
DISMM4 LD A,255
 LD (DISMOD),A
 LD (DISADR),HL
 PUSH HL
 CALL CLRBUF
 POP HL
 LD A,H
 CALL BUFBYT
 LD A,L
 CALL BUFBYT
 LD A,' '
 CALL PUTBUF
 LD BC,OUTBUF+15
 LD (BUFPNT),BC
 CALL DISAS1
 LD A,(DISNMB)
 OR A
 JR NZ,DISMM2
 INC A
DISMM2 LD B,A
 LD DE,(BUFPNT)
 PUSH DE
 LD HL,OUTBUF+5
 LD (BUFPNT),HL
 LD HL,(DISADR)
DISMM3 LD A,(HL)
 CALL BUFBYT
 INC HL
 DJNZ DISMM3
 POP DE
 LD (BUFPNT),DE
 RET 
;
DISMEM CALL DISMM4
 PUSH HL
 CALL DISBUF
 POP HL
 LD A,13
 JP CHPUT1
;
DISSRC CALL DISSRS
 LD A,37
 JP NICEMESG
;
DISSRS XOR A
 LD (DISMOD),A
 CALL CLRBUF
 PUSH HL
 LD DE,(LINCNT)
 CALL LINENO
 POP HL
 PUSH HL
 LD A,(HL)
 AND 80H
 JR NZ,DISSR2
 INC HL
 LD A,(HL)
 CP 0EDH
 JR NZ,DISSR5
 INC HL
 LD A,(HL)
 CP 6
 JR NZ,DISSR5
 POP HL
 LD A,';'
 CALL PUTBUF
 LD A,(HL)
 INC HL
 INC HL
 SUB 2
 RET Z
 LD B,A
 JR DISSR1
DISSR5 POP HL
 PUSH HL
 LD B,8
DISSR3 LD A,' '
 CALL PUTBUF
 DJNZ DISSR3
 JR DISSR4
DISSR2 INC HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 CALL SPCLBL
DISSR4 LD A,' '
 CALL PUTBUF
 INC HL
 CALL DISAS1
 POP DE
 CP A
 PUSH HL
 SBC HL,DE
 LD A,(DE)
 AND 7FH
 SUB L
 POP HL
 RET Z
 LD B,A
 PUSH HL
 LD A,(LINKFLAG)
 OR A
 JR NZ,DISSR6
 LD HL,(BUFPNT)
 LD DE,OUTBUF+36
 CP A
 SBC HL,DE
 JR NC,DISSR6
 LD (BUFPNT),DE
DISSR6 POP HL
 LD A,';'
 CALL PUTBUF
DISSR1 INC HL
 LD A,(HL)
 CALL PUTBUF
 DJNZ DISSR1
 RET 
;
DISAS1 CALL FORMAT
 LD (CNSTIF),BC
 LD A,B
 OR A
 RET Z
;
DIS2 LD BC,DISTBL
 JP FORMT1
;
DISTBL DEFW DISS0
 DEFW DISS1
 DEFW DISS2
 DEFW DISS3
 DEFW DISS4
 DEFW DISS5
 DEFW DISS6
 DEFW DISS7
;
 DEFW DISS8
 DEFW DISS9
 DEFW DISSA
 DEFW DISSB
 DEFW DISSC
 DEFW DISSD
 DEFW DISSE
 DEFW DISSF
;
 DEFW DISS10
 DEFW DISS11
;
MSEXAF DEFM 'EX   AF,AF'
 DEFB '''
 DEFB 128
;
DISS0 LD B,A
 OR A
 LD A,28
 JP Z,DISOPC
 LD A,B
 CP 8
 JP NZ,DISS03
 PUSH HL
 LD HL,MSEXAF
 CALL DISOP3
 POP HL
 RET 
DISS03 LD A,B
 CP 16
 JR NZ,DISS02
 LD A,54
 CALL DISOPC
 JR DISS01
DISS02 LD A,53
 CALL DISOPC
 LD A,B
 CP 18H
 JR Z,DISS01
 AND 18H
 CALL DISFLG
 CALL DISKMA
DISS01 INC HL
 LD A,(DISMOD)
 INC A
 JP NZ,DISEXP
 LD C,(HL)
 INC HL
 LD B,0
 BIT 7,C
 JR Z,DISS04
 DEC B
DISS04 ADD HL,BC
 EX DE,HL
 JP DISHEX
;
DISS1 BIT 3,A
 LD B,A
 JR NZ,DISS13
 CALL DISLD
 LD A,B
 CALL DISRG9
 CALL DISKMA
 INC HL
 JP DISEXP
DISS13 LD A,13
 CALL DISOPC
 CALL DISHL
 CALL DISKMA
 LD A,B
 JP DISRG9
;
DISS2 LD B,A
 CALL DISLD
 LD A,B
 BIT 5,A
 JR NZ,DISS21
 BIT 3,A
 JR NZ,DISS22
 CALL DISS28
 CALL DISKMA
DISS23 LD A,'A'
 JP PUTBUF
DISS22 CALL DISS23
 CALL DISKMA
DISS28 LD A,'('
 CALL PUTBUF
 LD A,B
 AND 16
 CALL DISRG9
 LD A,')'
 JP PUTBUF
DISS21 BIT 3,A
 JR NZ,DISS25
 CALL DISS26
 CALL DISKMA
DISS27 LD A,B
 BIT 4,A
 LD A,'A'
 JP NZ,PUTBUF
 LD A,20H
 JP DISRG9
DISS25 CALL DISS27
 CALL DISKMA
DISS26 LD A,'('
 CALL PUTBUF
 INC HL
 PUSH BC
 CALL DISEXP
 POP BC
 LD A,')'
 JP PUTBUF
;
DISS3 LD B,A
 RRA 
 RRA 
 RRA 
 AND 1
 ADD A,21
 CALL DISOPC
 LD A,B
 JP DISRG9
;
DISS4 LD B,A
 LD A,21
 JR DISS61
;
DISS5 LD B,A
 LD A,22
DISS61 CALL DISOPC
 LD A,B
 JP DISRG5
;
DISS6 LD B,A
 CALL DISLD
 LD A,B
 CALL DISRG5
 CALL DISKMA
 INC HL
 JP DISEXP
;
DISS7 LD B,35
 BIT 5,A
 JR Z,DISS71
 LD B,23
DISS71 RRA 
 RRA 
 RRA 
 AND 3
 ADD A,B
 JP DISOPC
;
DISS8 LD B,A
 LD A,56
 CALL DISOPC
 LD A,B
 JP DISFLG
;
DISS9 BIT 3,A
 LD B,A
 JR NZ,DISS91
 LD A,1
DISS92 CALL DISOPC
 LD A,B
 JP DISRGA
DISS91 LD DE,TBLDS9
 RRA 
 RRA 
 RRA 
 RRA 
 AND 3
 LD B,A
 CALL DISTB
 LD A,B
 CP 2
 RET C
 CALL DISHL
 LD A,B
 CP 2
 RET NZ
 LD A,')'
 JP PUTBUF
;
TBLDS9 DEFB 0D2H
 DEFM 'ET  '
 DEFB 0C5H
 DEFM 'XX  '
 DEFB 0CAH
 DEFM 'P   ('
 DEFB 0CCH
 DEFM 'D   SP,'
 DEFB 128
;
DISSA LD B,A
 LD A,52
DISSA1 CALL DISOPC
 LD A,B
 CALL DISFLG
 CALL DISKMA
 INC HL
 JP DISEXP
;
DISSB RRA 
 RRA 
 RRA 
 AND 7
 LD DE,ADRTBB
 JP JPVAR
;
ADRTBB DEFW DISSB1
 DEFW DISSB2
 DEFW DISSB3
 DEFW DISSB4
 DEFW DISSB5
 DEFW DISSB6
 DEFW DISSB7
 DEFW DISSB8
;
DISSB1 LD A,52
DISB11 CALL DISOPC
 INC HL
 JP DISEXP
;
DISSB2 INC HL
 LD A,(INDEX)
 OR A
 LD A,(HL)
 JR Z,DISB21
 PUSH HL
 CALL SKPEXP
 LD A,(HL)
 POP HL
DISB21 DEC HL
 LD B,A
 CP 40H
 JR NC,DISB22
 RRA 
 RRA 
 RRA 
 AND 7
 ADD A,39
 CALL DISOPC
 LD A,B
 ADD A,A
 ADD A,A
 ADD A,A
 CALL DISRG5
 INC HL
 RET 
DISB22 RLCA 
 RLCA 
 AND 3
 ADD A,48
 CALL DISOPC
 LD A,B
 RRA 
 RRA 
 RRA 
 AND 7
 ADD A,'0'
 CALL PUTBUF
 CALL DISKMA
 LD A,B
 ADD A,A
 ADD A,A
 ADD A,A
 CALL DISRG5
 INC HL
 RET 
;
DISSB3 LD A,61
 CALL DISOPC
 CALL DISB41
 CALL DISKMA
 LD A,'A'
 JP PUTBUF
;
DISSB4 LD A,60
 CALL DISOPC
 LD A,'A'
 CALL PUTBUF
 CALL DISKMA
DISB41 JP DISS26
;
DISSB5 XOR A
 CALL DISB61
 JP DISHL
;
DISSB6 LD A,1
DISB61 LD DE,EXMS
 JP DISTB
;
EXMS DEFB 0C5H
 DEFM 'X   (SP),'
 DEFB 0C5H
 DEFM 'X   DE,HL'
 DEFB 128
;
DISSB7 LD A,30
 JP DISOPC
;
DISSB8 LD A,31
 JP DISOPC
;
JPVAR PUSH HL
 LD L,A
 LD H,0
 ADD HL,HL
 ADD HL,DE
 LD E,(HL)
 INC HL
 LD D,(HL)
 POP HL
 PUSH DE
 RET 
;
DISSC LD B,A
 LD A,55
 JP DISSA1
;
DISSD BIT 3,A
 JR NZ,DISSD2
 LD B,A
 LD A,2
 JP DISS92
DISSD2 CP 0DDH
 JP Z,DISSD1
 CP 0FDH
 JP Z,DISSD1
 CP 0CDH
 LD A,55
 JP Z,DISB11
 INC HL
 LD A,(HL)
 LD B,A
 CP 6
 JR Z,DISSD3
 CP MAXPSD+1
 JR NC,DISSD3
 ADD A,70
 CALL DISOPC
 LD A,B
 CP 7
 RET Z
 INC HL
 CP 2
 JP NZ,DISEXP
 LD B,(HL)
 LD A,27H
 CALL PUTBUF
DISSD4 INC HL
 LD A,(HL)
 CALL PUTBUF
 DJNZ DISSD4
 LD A,27H
 JP PUTBUF
DISSD3 AND 0C7H
 CP 43H
 LD A,B
 JR NZ,DISSD5
 CALL DISLD
 LD A,B
 BIT 3,A
 JR Z,DISSD6
 CALL DISRG9
 CALL DISKMA
 JP DISS26
DISSD6 CALL DISS26
 CALL DISKMA
 LD A,B
 JP DISRG9
DISSD5 AND 0E7H
 CP 47H
 LD A,B
 JR NZ,DISSD7
 CALL DISLD
 LD A,B
 RRA 
 RRA 
 RRA 
 AND 3
 LD DE,DIS5TB
 JP DISTB
DISSD7 LD A,B
 AND 0E4H
 CP 0A0H
 LD A,B
 JR NZ,DISSD8
 RRA 
 RRA 
 RRA 
 AND 3
 BIT 1,B
 JR NZ,DISSD9
 BIT 0,B
 LD C,5
 JR Z,DISSDA
 LD C,9
DISSDA ADD A,C
 JP DISOPC
DISSD9 BIT 0,B
 LD C,62
 JR Z,DISSDA
 LD C,66
 JR DISSDA
DISSD8 AND 0C7H
 CP 42H
 LD A,B
 JR NZ,DISSDB
 BIT 3,B
 LD A,14
 JR NZ,DISSDC
 LD A,16
DISSDC CALL DISOPC
 CALL DISHL
 CALL DISKMA
 LD A,B
 JP DISRG9
DISSDB AND 0C6H
 CP 40H
 LD A,B
 JR NZ,DISSDD
 BIT 0,A
 LD A,60
 JR Z,DISSDE
 INC A
DISSDE CALL DISOPC
 BIT 0,B
 JR NZ,DISSDF
 LD A,B
 CALL DISRG5
 CALL DISKMA
DISSDG LD DE,INOTMS
 XOR A
 JP DISTB
DISSDF CALL DISSDG
 CALL DISKMA
 LD A,B
 JP DISRG5
DISSDD PUSH HL
 CALL FORMD8
 POP HL
 LD A,E
 LD DE,EXPTBL
 JP DISTB
;
INOTMS DEFB 0A8H
 DEFM 'C)'
 DEFB 128
;
DIS5TB DEFB 0C9H
 DEFM ',A'
 DEFB 0D2H
 DEFM ',A'
 DEFB 0C1H
 DEFM ',I'
 DEFB 0C1H
 DEFM ',R'
 DEFB 128
;
DISSD1 INC HL
 JP DIS2
;
DISSE LD B,A
 CALL DISS14
 INC HL
 JP DISEXP
;
DISSF LD B,A
 LD A,59
 CALL DISOPC
 INC HL
 LD A,(DISMOD)
 INC A
 JP NZ,DISEXP
 LD A,B
 AND 38H
 LD E,A
 LD D,0
 JP DISHEX
DISS10 CP 76H
 LD B,A
 LD A,29
 JP Z,DISOPC
 CALL DISLD
 LD A,B
 CALL DISRG5
 CALL DISKMA
 LD A,B
 ADD A,A
 ADD A,A
 ADD A,A
 JP DISRG5
;
DISS14 RRA 
 RRA 
 RRA 
 AND 7
 LD C,A
 ADD A,13
 CALL DISOPC
 LD A,C
 CP 4
 RET NC
 CP 2
 RET Z
 LD A,'A'
 CALL PUTBUF
 JP DISKMA
;
DISS11 LD B,A
 CALL DISS14
DISS12 LD A,B
 ADD A,A
 ADD A,A
 ADD A,A
 JP DISRG5
;
DISEXP XOR A
 LD (BRACKETS),A
 LD A,(DISMOD)
 INC A
 JR NZ,DSEXP2
 LD A,(CNSTIF)
DSEXP3 RRA 
 RRA 
 LD B,A
 AND 3
 LD A,B
 JR Z,DSEXP3
 AND 3
 LD E,(HL)
 INC HL
 DEC A
 LD D,0
 JR Z,DSEXP4
 LD D,(HL)
 INC HL
DSEXP4 PUSH HL
 CALL DISHEX
 POP HL
 DEC HL
 RET 
DSEXP2 LD A,(HL)
 LD E,A
 AND 0DFH
 PUSH AF
 INC HL
 CP 0D0H
 LD A,E
 JR NC,DSEXP5
 AND 0D0H
 CP 90H
 LD A,E
 JR Z,DSEXP5
 LD E,(HL)
 INC HL
 LD D,0
 BIT 3,A
 JR NZ,DSEXP5
 LD D,(HL)
 INC HL
DSEXP5 PUSH HL
 CALL DISTRM
 POP HL
 POP AF
 CP 0D0H
 JR NC,DSEXP2
 AND 7
 JR Z,DSEXP7
 LD E,A
 LD A,(BRACKETS)
 LD D,A
DSEXP6 LD A,(HL)
 CP 0D1H
 JR NZ,DSEXP9
 INC HL
 DEC D
 LD A,')'
 CALL PUTBUF
 JR DSEXP6
DSEXP9 LD A,D
 LD (BRACKETS),A
 DEC E
 LD A,'+'
 JR Z,DSEXP1
 DEC E
 LD A,'-'
 JR Z,DSEXP1
 DEC E
 LD A,'/'
 JR Z,DSEXP1
 DEC E
 LD A,'*'
 JR Z,DSEXP1
 DEC E
 LD A,'&'
 JR Z,DSEXP1
 LD A,'!'
DSEXP1 CALL PUTBUF
 JR DSEXP2
DSEXP7 DEC HL
 LD A,(BRACKETS)
 OR A
 RET Z
 LD B,A
 LD A,')'
DSEXP8 CALL PUTBUF
 DJNZ DSEXP8
 RET 
;
DISTRM LD B,A
 BIT 5,A
 JR Z,DISTR1
 LD A,'-'
 CALL PUTBUF
 LD A,B
DISTR1 AND 0D0H
 JP Z,DISDEC
 CP 90H
 JR NZ,DISTR3
 LD A,'$'
 JP PUTBUF
DISTR3 CP 0D0H
 JR NZ,DISTR2
 LD A,'('
 CALL PUTBUF
 LD A,(BRACKETS)
 INC A
 LD (BRACKETS),A
 RET 
DISTR2 CP 40H
 JP Z,DISHEX
 CP 10H
 JP Z,DISOCT
 CP 50H
 JP Z,DISBIN
 CP 80H
 JP NZ,DISLBL
 LD A,27H
 CALL PUTBUF
 LD A,E
 CALL PUTBUF
 LD A,27H
 JP PUTBUF
;
DISHEX LD HL,VALBUF
 LD A,D
 CALL DSBYT2
 LD A,E
 CALL DSBYT2
 XOR A
 LD (HL),A
 PUSH DE
 LD DE,10
 LD A,'H'
 JR DISOCT2
;
DISOCT EX DE,HL
 PUSH HL
 LD DE,VALBUF
 XOR A
 LD B,6
 JR DISOCT1
DISOCTLP XOR A
 ADD HL,HL
 RLA 
 ADD HL,HL
 RLA 
DISOCT1 ADD HL,HL
 RLA 
 ADD A,'0'
 LD (DE),A
 INC DE
 DJNZ DISOCTLP
 XOR A
 LD (DE),A
 LD DE,8
 LD A,'O'
DISOCT2 PUSH AF
 CALL DISVAL
 POP AF
 POP HL
 CP A
 SBC HL,DE
 RET C
 JP PUTBUF
;
DISBIN EX DE,HL
 PUSH HL
 LD DE,VALBUF
 LD B,16
DISBINLP XOR A
 ADD HL,HL
 ADC A,'0'
 LD (DE),A
 INC DE
 DJNZ DISBINLP
 XOR A
 LD (DE),A
 LD DE,2
 LD A,'B'
 JR DISOCT2
;
DISDC5 LD HL,0
 PUSH HL
 LD A,5
 LD HL,1
DISDC1 PUSH HL
 LD C,L
 LD B,H
 ADD HL,HL
 ADD HL,HL
 ADD HL,BC
 ADD HL,HL
 DEC A
 JR NZ,DISDC1
 EX DE,HL
 LD BC,VALBUF
DISDC2 POP DE
 LD A,E
 OR A
 JR Z,DISDC4
 LD A,2FH
DISDC3 INC A
 SBC HL,DE
 JR NC,DISDC3
 ADD HL,DE
 LD (BC),A
 INC BC
 JR DISDC2
DISDC4 LD (BC),A
 RET 
;
LINENO CALL DISDC5
 LD HL,VALBUF+1
 LD B,4
LINNO1 LD A,(HL)
 CALL PUTBUF
 INC HL
 DJNZ LINNO1
 LD A,' '
 JP PUTBUF
;
DISDEC CALL DISDC5
;
DISVAL LD HL,VALBUF
DSVAL1 LD A,(HL)
 INC HL
 CP '0'
 JR Z,DSVAL1
 OR A
 JR NZ,DSVAL2
 LD A,'0'
 JP PUTBUF
DSVAL2 CALL TYPCHR
 JR Z,DSVAL3
 PUSH AF
 LD A,'0'
 CALL PUTBUF
 POP AF
DSVAL3 CALL PUTBUF
 LD A,(HL)
 INC HL
 OR A
 JR NZ,DSVAL3
 RET 
DSBYT2 PUSH AF
 RRA 
 RRA 
 RRA 
 RRA 
 CALL DSNBL2
 POP AF
DSNBL2 AND 15
 ADD A,'0'
 CP 3AH
 JR C,DSNBL3
 ADD A,7
DSNBL3 LD (HL),A
 INC HL
 RET 
;
BUFBYT PUSH HL
 LD HL,(BUFPNT)
 CALL DSBYT2
 LD (BUFPNT),HL
 POP HL
 RET 
;
LOCLBL DEC HL
 LD C,L
 LD B,H
 ADD HL,HL
 ADD HL,BC
 ADD HL,HL
 ADD HL,HL
 LD A,(LBLPAGE)
 OUT (0FDH),A
 LD BC,(LBLADR)
DISLB2 ADD HL,BC
 RET 
;
DISLB1 EX DE,HL
 CALL LOCLBL
 PUSH DE
 LD DE,LBLBUF
 LD BC,8
 LDIR 
 POP DE
 LD B,8
 LD HL,LBLBUF
;
; FLAGS INVARIANT
RSTMEM LD A,2
 OUT (0FDH),A
 RET 
;
SPCLBL PUSH HL
 CALL DISLB1
SPCLB1 LD A,(HL)
 INC HL
 CALL PUTBUF
 DJNZ SPCLB1
 POP HL
 RET 
;
DISLBL CALL DISLB1
DSLBL1 LD A,(HL)
 CP ' '
 RET Z
 CALL PUTBUF
 INC HL
 DJNZ DSLBL1
 RET 
;
FORMAT LD BC,2
 LD (INDEXT),BC
 LD BC,FRMTBL
 PUSH HL
 CALL FORMT1
 POP HL
 INC B
 DEC B
 RET 
;
FORMT1 PUSH HL
 PUSH DE
 LD A,(HL)
 CP 0C0H
 JR NC,FORMT2
 CP 40H
 JR C,FORMT2
 LD HL,16
 CP 80H
 JR C,FORMT3
 INC HL
 JR FORMT3
FORMT2 LD D,A
 AND 7
 BIT 6,D
 JR Z,FORMT4
 ADD A,8
FORMT4 LD L,A
 LD H,0
 LD A,D
FORMT3 ADD HL,HL
 ADD HL,BC
 LD C,(HL)
 INC HL
 LD B,(HL)
 POP DE
 POP HL
 PUSH BC
 RET 
;
FRMTBL DEFW FORM0
 DEFW FORM1
 DEFW FORM2
 DEFW FORM3
 DEFW FORM3
 DEFW FORM3
 DEFW FORM6
 DEFW FORM3
;
 DEFW FORM3
 DEFW FORM3
 DEFW FORMA
 DEFW FORMB
 DEFW FORMC
 DEFW FORMD
 DEFW FORME
 DEFW FORM3
;
 DEFW FORM3
 DEFW FORM3
;
FORM0 LD BC,100H
 CP 10H
 RET C
 LD BC,204H
 RET 
;
FORM1 LD BC,30CH
 BIT 3,A
 RET Z
 LD BC,100H
 RET 
;
FORM2 LD BC,100H
 BIT 5,A
 RET Z
 LD BC,30CH
 RET 
;
FORM3 LD BC,100H
 RET 
;
FORM6 LD BC,204H
 RET 
;
FORMA LD BC,30CH
 RET 
;
FORMB LD BC,30CH
 CP 0C3H
 RET Z
 LD BC,100H
 CP 0E3H
 RET NC
 LD BC,204H
 CP 0CBH
 RET NZ
 INC HL
 LD A,(HL)
 LD BC,200H
 RET 
;
FORMC LD BC,30CH
 RET 
;
FORMD LD BC,100H
 BIT 3,A
 RET Z
 LD BC,30CH
 CP 0CDH
 RET Z
 CP 0EDH
 JR NZ,FORMD5
 INC HL
 LD A,(HL)
 LD D,A
 LD BC,200H
 CP MAXPSD+1
 JR NC,FORMD6
 CP 2
 JR Z,FORMD9
 CP 6
 JR Z,FORMD9
 CP 7
 JR Z,FORMD9
 LD BC,310H
FORMD9 LD A,(DISMOD)
 OR A
 RET Z
 INC A
FORMD6 AND 0FEH
 CP 70H
 JR NZ,FORMDA
 LD BC,0
 RET 
FORMDA LD A,D
 AND 0E7H
 CP 47H
 RET Z
 AND 0C7H
 CP 42H
 RET Z
 AND 0F6H
 CP 40H
 RET Z
 LD A,D
 AND 0E4H
 CP 0A0H
 RET Z
 LD BC,430H
 LD A,D
 AND 0C7H
 CP 43H
 RET Z
FORMD8 LD HL,FRMDT1
 LD A,D
 JP FORMD7
FORMD5 LD (INDEX),A
 INC HL
 LD A,(HL)
 LD B,A
 AND 0C0H
 CP 40H
 JR NZ,FORMD1
 LD A,B
 CALL FRMCHK
 LD C,A
 LD A,B
 RRA 
 RRA 
 RRA 
 CALL FRMCHK
 OR C
 LD (INDEXT),A
 LD BC,0
 RET Z
 LD BC,200H
 DEC A
 RET Z
 LD BC,310H
 RET 
FORMD1 CP 80H
 JR NZ,FORMD2
 LD A,B
 CALL FRMCHK
 LD (INDEXT),A
 LD BC,0
 RET Z
 DEC A
 LD BC,200H
 RET Z
 LD BC,310H
 RET 
FORMD2 LD A,B
 CP 0CBH
 JR NZ,FORMD4
 INC HL
 CALL SKPEXP
 LD A,(HL)
 LD BC,410H
 CP 40H
 RET NC
 AND 38H
 CP 30H
 RET NZ
 LD BC,0
 RET 
FORMD4 LD BC,430H
 CP 22H
 RET Z
 CP 21H
 RET Z
 CP 2AH
 RET Z
 LD BC,310H
 CP 35H
 RET Z
 CP 34H
 RET Z
 LD D,A
 LD A,1
 LD (INDEXT),A
 LD A,D
 CP 26H
 RET Z
 CP 2EH
 RET Z
 LD BC,450H
 CP 36H
 RET Z
 LD HL,FRMDTB
FORMD7 LD D,A
 LD BC,0
 LD E,C
FORMD3 LD A,(HL)
 OR A
 RET Z
 INC HL
 INC E
 CP D
 JR NZ,FORMD3
 LD BC,200H
 DEC E
 RET 
;
FRMDTB DEFW 0E1E5H
 DEFW 919H
 DEFW 2939H
 DEFW 232BH
 DEFW 0F9E9H
 DEFW 2425H
 DEFW 2C2DH
 DEFW 0E3H
;
FRMDT1 DEFM 'DFVME'
 DEFW 6F5EH
 DEFW 67H
;
EXPTBL DEFB 0CEH
 DEFM 'EG '
 DEFB 0C9H
 DEFM 'M 0'
 DEFB 0C9H
 DEFM 'M 1'
 DEFB 0D2H
 DEFM 'ETI '
 DEFB 0D2H
 DEFM 'ETN '
 DEFB 0C9H
 DEFM 'M 2'
 DEFB 0D2H
 DEFM 'LD '
 DEFB 0D2H
 DEFM 'RD '
 NOP 
;
FRMCHK AND 7
 CP 7
 JP Z,RETZRO
 CP 4
 JP C,RETZRO
 CP 6
 LD A,1
 RET NZ
 INC A
 RET 
;
FORME LD BC,204H
 RET 
;
;
DISBYT PUSH AF
 RRA 
 RRA 
 RRA 
 RRA 
 CALL DISNBL
 POP AF
;
DISNBL AND 15
 ADD A,'0'
 CP 3AH
 JR C,DISNB1
 ADD A,7
DISNB1 JP CHPUT1
;
CLRSRC LD HL,0
 LD (LBLCNT),HL
 LD HL,(SRCADR)
 LD (ENDTXT),HL
 LD (HL),0
 LD A,(LBLPAGE)
 OUT (0FDH),A
 LD HL,(LBLADR)
 LD (HL),0
 LD A,2
 OUT (0FDH),A
 RET 
;
; HL=ADDR V LABEL
;
FDLBLS LD HL,1
 CALL LOCLBL
FNDLB3 LD BC,0
 LD (FRELBL),BC
FDLBL1 INC BC
 LD A,(HL)
 OR A
 JR Z,FDLBL9
 DEC A
 JR NZ,FDLBL3
 LD (FRELBL),BC
FDLBL4 PUSH BC
 LD BC,12
 ADD HL,BC
 POP BC
 JR FDLBL1
FDLBL3 PUSH BC
 PUSH HL
 LD DE,LBLBUF
 LD B,8
FDLBL5 LD A,(DE)
 CP (HL)
 JR NZ,FDLBL6
 INC HL
 INC DE
 DJNZ FDLBL5
FDLBL6 POP HL
 POP BC
 JR NZ,FDLBL4
 OR A
FDLBL9 RET 
;
FNDLBL CALL SKPLBL
 PUSH HL
 CALL FDLBLS
 EX DE,HL
 JR NZ,FDLBLA
FDLBL2 LD HL,(FRELBL)
 LD A,H
 OR L
 JR Z,FDLBL7
 PUSH HL
 CALL LOCLBL
 POP BC
 JR FDLBL8
FDLBL7 LD HL,12
 ADD HL,DE
 LD (HL),0
 EX DE,HL
FDLBL8 PUSH HL
 EX DE,HL
 LD HL,LBLBUF
 PUSH BC
 LD BC,8
 LDIR 
 XOR A
 LD (DE),A
 INC DE
 LD (DE),A
 POP BC
 POP DE
 LD HL,(LBLCNT)
 INC HL
 LD (LBLCNT),HL
FDLBLA POP HL
 RET 
;
ADDLBL CALL FNDLBL
 LD HL,8
 ADD HL,DE
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC DE
 LD (HL),D
 DEC HL
 LD (HL),E
 JP RSTMEM
;
DELLBL PUSH AF
 PUSH HL
 PUSH BC
 EX DE,HL
 CALL LOCLBL
 LD BC,8
 PUSH HL
 ADD HL,BC
 LD C,(HL)
 INC HL
 LD B,(HL)
 RES 7,B
 DEC BC
 LD (HL),B
 DEC HL
 LD (HL),C
 POP HL
 LD A,B
 OR C
 JR NZ,DLLBL2
 INC A
 LD (HL),A
 LD HL,(LBLCNT)
 DEC HL
 LD (LBLCNT),HL
DLLBL2 POP BC
 POP HL
 LD A,2
 OUT (0FDH),A
DLLBL3 POP AF
 RET 
;
FNDLINER CALL FNDLIN
 RET NZ
DISBAD PUSH HL
 LD HL,BADMES
 CALL MESAGE
 POP HL
 RET 
;
BADMES DEFM '*** Bad'
 DEFW 13
;
;
FNDLIN LD A,D
 OR E
 RET Z
 LD HL,(SRCADR)
FNDLN1 LD A,(HL)
 AND 7FH
 RET Z
 LD C,A
 DEC DE
 LD A,D
 OR E
 JR Z,FNDLN2
 LD B,0
 ADD HL,BC
 INC HL
 JR FNDLN1
FNDLN2 INC A
 RET 
;
INPLIN CALL CLRBUF
 LD DE,(LINCNT)
 CALL LINENO
 CALL LINEDIT
 CP 3
 RET 
;
DISLNO CALL CLRBUF
 CALL LINENO
 JP DISBUF
;
ASKADR LD HL,ASKADRMS
 CALL MESAGE
 LD HL,INPBUF
 PUSH HL
 LD B,5
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 EX DE,HL
 LD HL,0
 CALL EVLCSH
 XOR A
 RET 
;
ASKADRMS DEFM 'Address (Hex) ? '
 NOP 
;
ASKILN LD HL,ILNQMS
 JR ASKLN2
;
ASKLLN LD HL,LLNQMS
 JR ASKLN2
;
ASKFLN LD HL,FLNQMS
 JR ASKLN2
;
ASKLIN LD HL,LINQMS
ASKLN2 CALL MESAGE
 LD HL,INPBUF
 LD B,11
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CP 13
 JR NZ,ASKLNB
 LD DE,(DEFLINE)
 RET 
ASKLNB CALL TYPCHR
 JR C,ASKLN5
 JP Z,EVLCNS
 CALL SKPLBL
 JR Z,ASKLN5
 PUSH HL
 CALL FDLBLS
 PUSH AF
 CALL RSTMEM
 POP AF
 POP DE
 JR Z,ASKLN5
 PUSH DE
 PUSH BC
 LD DE,0
 PUSH DE
 INC DE
 CALL FNDLINER
 POP DE
 POP BC
ASKLN3 LD A,(HL)
 OR A
 JR Z,ASKLN1
 INC DE
 CP 80H
 JR C,ASKLN4
 PUSH HL
 INC HL
 PUSH DE
 LD E,(HL)
 INC HL
 LD D,(HL)
 EX DE,HL
 SBC HL,BC
 POP DE
 POP HL
 JR NZ,ASKLN4
 POP HL
 CALL NXTCHR
 CP '-'
 JR Z,ASKLN7
 CP '+'
 JR NZ,ASKLN6
ASKLN7 PUSH AF
 CALL NXTCHI
 PUSH DE
 CALL EVLCNS
 POP HL
 JR NZ,ASKLN8
ASKLN1 POP AF
 JR ASKLN5
ASKLN8 POP AF
 CP '-'
 JR NZ,ASKLN9
 SBC HL,DE
 JR ASKLNA
ASKLN9 ADD HL,DE
ASKLNA EX DE,HL
 JR C,ASKLN5
ASKLN6 XOR A
 NEG 
 RET 
ASKLN4 AND 7FH
 PUSH BC
 LD C,A
 LD B,0
 ADD HL,BC
 POP BC
 INC HL
 JR ASKLN3
ASKLN5 LD DE,0FFFFH
 XOR A
 RET 
;
ILNQMS DEFM 'Insrt line#? '
 NOP 
;
LLNQMS DEFM 'Last  line#? '
 NOP 
;
FLNQMS DEFM 'First line#? '
 NOP 
;
LINQMS DEFM 'Line#? '
 NOP 
;
ERASUR LD DE,1
 CALL FNDLIN
 JR Z,ERSUR2
ERSUR1 LD HL,ERASMS
 CALL ASKYESNO
 CP 'Y'
 JP NZ,RETZRO
 CALL CLRSRC
ERSUR2 XOR A
 INC A
 RET 
;
ASKYESNO CALL MESAGE
ASKYESN1 CALL CHGET1
 CP 'Y'
 JR Z,ASKYESN2
 CP 'N'
 JR NZ,ASKYESN1
ASKYESN2 PUSH AF
 CALL CHPUT1
 LD A,13
 CALL CHPUT1
 POP AF
 RET 
;
ERASMS DEFM 'Code erasure.'
PROCEEDM DEFM ' Proceed (Y/N)? '
 NOP 
;
COMDCP CALL ERASUR
 RET Z
 XOR A
 LD (FILENAME),A
;
COMDCC LD DE,0FFFFH
 CALL FNDLIN
 PUSH HL
 LD HL,0
 SBC HL,DE
 LD (LINCNT),HL
 POP DE
CMDCC1 PUSH DE
 CALL INPLIN
 POP DE
 JR NZ,CMDCC2
 CP 3
 RET Z
 JR CMDCC1
CMDCC2 CALL INCLIN
 LD HL,ASMCNT
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 INC BC
 LDIR 
 XOR A
 LD (DE),A
 LD (ENDTXT),DE
 JR CMDCC1
;
COMDIS CALL ASKLIN
 CP 3
 RET Z
 PUSH DE
 CALL FNDLINER
 POP DE
 RET Z
COMDIS1 PUSH HL
 LD (LINCNT),DE
 LD DE,(ENDTXT)
 PUSH DE
 CALL CMDCC1
 POP DE
 LD HL,(ENDTXT)
 CP A
 SBC HL,DE
 LD C,L
 LD B,H
 POP HL
 EX DE,HL
 JP MOVEDWN
;
COMDDM CALL ASKFLN
 CP 3
 RET Z
 PUSH DE
 CALL FNDLINER
 POP DE
 RET Z
 PUSH HL
 PUSH DE
 CALL ASKLLN
;
 POP BC
 POP HL
 CP 3
 RET Z
 PUSH HL
 PUSH BC
;
 PUSH DE
 CALL FNDLINER
 POP HL
 POP DE
 POP BC
 RET Z
 CP A
 SBC HL,DE
 JP C,DISBAD
 PUSH BC
 LD C,L
 LD B,H
 INC BC
 POP HL
 PUSH HL
CMDDM1 PUSH BC
 CALL CMDDL6
 POP BC
 DEC BC
 LD A,B
 OR C
 JR NZ,CMDDM1
 EX DE,HL
 POP HL
 JR CMDDL7
;
COMDDL CALL ASKLIN
 CP 3
 RET Z
 CALL FNDLINER
 RET Z
COMDDL1 PUSH HL
 CALL CMDDL6
 EX DE,HL
 POP HL
;
CMDDL7 PUSH DE
 PUSH HL
 LD HL,(ENDTXT)
 CP A
 SBC HL,DE
 LD C,L
 LD B,H
 INC BC
 POP DE
 POP HL
 LDIR 
 DEC DE
 LD (ENDTXT),DE
 RET 
;
CMDDL6 PUSH HL
 LD A,(HL)
 INC HL
 CP 80H
 JR C,CMDDL5
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 CALL DELLBL
CMDDL5 XOR A
 LD (DISMOD),A
 CALL FORMAT
 LD A,(HL)
 CP 0C7H
 JR NZ,CMDDL1
 LD BC,204H; Exception : RST
CMDDL1 PUSH BC
 LD A,C
 AND 3
 JR Z,CMDDL3
 PUSH AF
CMDDL2 LD B,(HL)
 LD A,B
 INC HL
 AND 0D0H
 CP 90H; Skip $
 JR Z,CMDDL8
 CP 0D0H; Skip ( and )
 JR Z,CMDDL2
 LD E,(HL)
 INC HL
 LD D,0
 BIT 3,B
 JR NZ,CMDDL8
 LD D,(HL)
 INC HL
CMDDL8 CP 0C0H
 CALL Z,DELLBL
 LD A,B
 AND 7
 JR NZ,CMDDL2
 DEC HL
 POP AF
 DEC A
CMDDL3 POP BC
 JR Z,CMDDL4
 DEC B
CMDDL4 RR C
 RR C
 INC HL
 DJNZ CMDDL1
 POP HL
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 ADD HL,BC
 INC HL
 RET 
;
PARSFNAM LD DE,FCB
 XOR A
 LD C,A
 LD (DE),A
PRSFNM3 LD B,11
 LD A,'?'
PRSFNM1 INC DE
 LD (DE),A
 DJNZ PRSFNM1
 LD A,(HL)
 CP 33
 RET C
 LD DE,FCB+1
 LD B,8
 CALL PRSFNMSB
 CP ':'
 JR NZ,PRSFNM2
 LD A,(FCB)
 OR A
 RET NZ
 LD DE,FCB+1
 LD A,(DE)
 SUB 'A'
 RET C
 INC A
 DEC DE
 LD (DE),A
 LD C,1
 INC HL
 JR PRSFNM3
PRSFNM2 SET 1,C
 BIT 7,C
 JR Z,PRSFNM8
 SET 3,C
 RES 7,C
PRSFNM8 INC B
 DEC B
 JR Z,PRSFNM6
 LD A,' '
PRSFNM4 LD (DE),A
 INC DE
 DJNZ PRSFNM4
PRSFNM6 LD A,(HL)
 INC HL
 CP 33
 RET C
 CP '.'
 JR NZ,PRSFNM6
 RES 4,C
 SET 2,C
 PUSH DE
 LD B,3
 LD A,' '
PRSFNM7 LD (DE),A
 INC DE
 DJNZ PRSFNM7
 POP DE
 LD B,3
 CALL PRSFNMSB
 BIT 7,C
 RET Z
 RES 7,C
 SET 4,C
 RET 
;
PRSFNMSB LD A,(HL)
 CP ':'
 RET Z
 CP '.'
 RET Z
 CP 33
 RET C
 CP '?'
 JR NZ,PRSFNMS3
 SET 7,C
PRSFNMS3 CP '*'
 JR Z,PRSFNMS1
 LD (DE),A
 INC HL
 INC DE
 DJNZ PRSFNMSB
 RET 
PRSFNMS1 LD A,'?'
 SET 7,C
 INC HL
PRSFNMS2 LD (DE),A
 INC DE
 DJNZ PRSFNMS2
 RET 
;
COMDDR CALL SETLINK
 LD DE,(TRADDR)
 LD C,1AH
 CALL BDOS
 LD HL,FCB
 LD B,36
COMDDRL0 LD (HL),0
 INC HL
 DJNZ COMDDRL0
 LD HL,DIRMASK
 CALL MESAGE
 LD HL,INPBUF
 LD B,16
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL PARSFNAM
 LD C,11H
 LD DE,FCB
 CALL BDOS
 LD C,6
 OR A
 JR Z,COMDDRLP
 LD HL,NOTFNDMS
 CALL MESAGE
 JR COMDDR2
COMDDRLP LD HL,(TRADDR)
 INC HL
 LD B,8
COMDDRL1 LD A,(HL)
 CALL CHPUT1
 INC HL
 DJNZ COMDDRL1
 LD A,'.'
 CALL CHPUT1
 LD B,3
COMDDRL2 LD A,(HL)
 CALL CHPUT1
 INC HL
 DJNZ COMDDRL2
 LD A,' '
 CALL CHPUT1
 DEC C
 JR NZ,COMDDR1
 LD A,13
 CALL CHPUT1
 LD C,6
COMDDR1 PUSH BC
 LD C,12H
 LD DE,FCB
 CALL BDOS
 POP BC
 OR A
 JR Z,COMDDRLP
 LD A,C
 CP 6
 LD A,13
 CALL NZ,CHPUT1
COMDDR2 CALL CLRBUF
 LD A,(FCB)
 LD E,A
 LD C,1BH
 CALL BDOS
 INC A
 RET Z
 EX DE,HL
 CALL DISDEC
 CALL DISBUF
 LD HL,DRFREEMS
 JP MESAGE
;
DIRMASK DEFM 'Dir mask : '
 NOP 
;
DRFREEMS DEFM ' Kbytes free'
 DEFW 13
;
NOTFNDMS DEFM '*** File not found'
 DEFW 13
;
COMDLS LD HL,LBMASKMS
 CALL MESAGE
 LD HL,INPBUF
 LD B,8
 PUSH HL
 PUSH BC
 CALL INPUT
 POP BC
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CP ' '
 JR C,COMDLSB
 LD DE,INPBUF+24
 LD A,' '
 LD B,8
COMDLSL7 DEC DE
 LD (DE),A
 DJNZ COMDLSL7
 LD B,8
 CALL PRSFNMSB
 LD HL,1
 CALL LOCLBL
 LD BC,12
 LD DE,INPBUF+16
COMDLSL5 LD A,(HL)
 OR A
 JR Z,COMDLS8
 DEC A
 JR Z,COMDLSC
 PUSH HL
 PUSH DE
 PUSH BC
 LD B,8
COMDLSL6 LD A,(DE)
 CP '?'
 JR Z,COMDLSE
 CP (HL)
 JR NZ,COMDLSF
COMDLSE INC DE
 INC HL
 DJNZ COMDLSL6
COMDLSF POP BC
 POP DE
 POP HL
 JR NZ,COMDLSC
 SET 7,(HL)
COMDLSC ADD HL,BC
 JR COMDLSL5
COMDLSB LD HL,1
 CALL LOCLBL
 LD BC,12
COMDLSLP LD A,(HL)
 OR A
 JR Z,COMDLS8
 CP 1
 JR Z,COMDLS2
 OR 80H
 LD (HL),A
COMDLS2 ADD HL,BC
 JR COMDLSLP
COMDLS8 LD B,5
COMDLS1 PUSH BC
 LD BC,12
 LD HL,(LBLADR)
 LD DE,0
COMDLSL1 LD A,(HL)
 OR A
 JR Z,COMDLS3
 CP 80H
 JR C,COMDLS6
 LD A,D
 OR E
 JR Z,COMDLS5
 PUSH DE
 PUSH HL
COMDLSL2 LD A,(DE)
 CP (HL)
 INC HL
 INC DE
 JR Z,COMDLSL2
 POP HL
 POP DE
 JR C,COMDLS6
COMDLS5 LD E,L
 LD D,H
COMDLS6 ADD HL,BC
 JR COMDLSL1
COMDLS3 LD A,D
 OR E
 JR Z,COMDLS7
 LD A,(DE)
 AND 7FH
 LD (DE),A
 LD B,8
COMDLSL3 LD A,(DE)
 CALL CHPUT1
 INC DE
 DJNZ COMDLSL3
 LD A,' '
 CALL CHPUT1
 INC DE
 LD A,(DE)
 CP 80H
 JR C,COMDLSA
 INC DE
 INC DE
 LD A,(DE)
 CALL DISBYT
 DEC DE
 LD A,(DE)
 CALL DISBYT
 LD B,2
 JR COMDLSL4
COMDLSA LD B,6
COMDLSL4 LD A,' '
 CALL CHPUT1
 DJNZ COMDLSL4
COMDLS9 POP BC
 CALL BREAKX
 JR C,COMDLSXT
 DJNZ COMDLS1
 LD A,13
 CALL CHPUT1
 LD B,5
 JR COMDLS1
COMDLS7 CALL RSTMEM
 POP AF
 CP 5
 LD A,13
 JP NZ,CHPUT1
 RET 
COMDLSXT LD HL,(LBLADR)
 LD BC,12
COMDLSX1 LD A,(HL)
 AND 7FH
 JR Z,COMDLSX2
 LD (HL),A
 ADD HL,BC
 JR COMDLSX1
COMDLSX2 LD A,13
 CALL CHPUT1
 JP RSTMEM
;
COMDBK CALL SHOWBRK
 LD A,(BRKPTCNT)
 CP BRKPTMAX
 RET Z
 CALL ASKADR
 CP 3
 RET Z
 LD HL,BRKPTTBL
 LD A,(BRKPTCNT)
 LD B,A
 OR A
 JR Z,COMDBK1
COMDBKLP PUSH BC
 LD C,(HL)
 INC HL
 LD B,(HL)
 DEC HL
 CP A
 PUSH DE
 EX DE,HL
 SBC HL,BC
 EX DE,HL
 POP DE
 POP BC
 RET Z
 JR C,COMDBK1
 INC HL
 INC HL
 INC HL
 DJNZ COMDBKLP
COMDBK1 PUSH HL
 PUSH DE
 LD DE,BRKPTMAX*3+BRKPTTBL-1
 LD HL,BRKPTMAX*3+BRKPTTBL-4
 LD A,(BRKPTCNT)
 SUB B
 LD B,A
 LD A,BRKPTMAX-1
 SUB B
 JR Z,COMDBK2
 LD C,A
 ADD A,A
 ADD A,C
 LD C,A
 LD B,0
 LDDR 
COMDBK2 POP DE
 POP HL
 LD (HL),E
 INC HL
 LD (HL),D
 LD HL,BRKPTCNT
 INC (HL)
 RET 
;
SHOWBRK LD A,(BRKPTCNT)
 OR A
 RET Z
 LD B,A
 LD HL,BRKPTMES
 CALL MESAGE
 LD HL,BRKPTTBL
SHOWBRK1 LD A,(BRKPTCNT)
 ADD A,'1'
 SUB B
 CALL CHPUT1
 LD A,' '
 CALL CHPUT1
 LD C,(HL)
 INC HL
 LD A,(HL)
 CALL DISBYT
 LD A,C
 CALL DISBYT
 INC HL
 INC HL
 LD A,13
 CALL CHPUT1
 DJNZ SHOWBRK1
 RET 
;
BRKPTMES DEFM 'Breakpoints :'
 DEFW 13
;
COMDCB LD A,(BRKPTCNT)
 OR A
 RET Z
 CALL SHOWBRK
 LD HL,BRKNOMES
 CALL MESAGE
 LD HL,INPBUF
 LD B,1
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 LD A,(HL)
 CP 'A'
 JR NZ,COMDCB1
 XOR A
 LD (BRKPTCNT),A
 RET 
COMDCB1 SUB '1'
 JP C,DISBAD
 LD B,A
 LD A,(BRKPTCNT)
 DEC A
 CP B
 JP C,DISBAD
 LD E,B
 LD C,B
 LD B,0
 LD HL,BRKPTTBL
 ADD HL,BC
 ADD HL,BC
 ADD HL,BC
 LD C,E
 LD E,L
 LD D,H
 INC HL
 INC HL
 INC HL
 LD A,BRKPTMAX
 SUB C
 LD C,A
 ADD A,A
 ADD A,C
 LD C,A
 LD B,0
 LDIR 
 LD HL,BRKPTCNT
 DEC (HL)
 RET 
;
BRKNOMES DEFM 'Breakpoint no. ? '
 NOP 
;
COMDCV LD HL,VALMES
 CALL MESAGE
 LD HL,INPBUF
 LD B,18
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CALL EVLCNS
 JP Z,DISBAD
 CALL CLRBUF
 PUSH DE
 CALL DISHEX
 LD HL,OUTBUF+8
 LD (BUFPNT),HL
 POP DE
 PUSH DE
 CALL DISDEC
 LD HL,OUTBUF+16
 LD (BUFPNT),HL
 POP DE
 PUSH DE
 CALL DISOCT
 LD HL,OUTBUF+24
 LD (BUFPNT),HL
 POP DE
 CALL DISBIN
 LD A,13
 CALL PUTBUF
 JP DISBUF
;
COMDAI CALL ASKADR
 CP 3
 RET Z
 EX DE,HL
COMDAILP PUSH HL
 CALL DISMM4
 LD HL,OUTBUF+39
 LD (BUFPNT),HL
 CALL DISBUF
 LD HL,INPBUF
 LD B,40
 CALL INPUT
 POP HL
 CP 3
 RET Z
 PUSH HL
 LD HL,INPBUF
 CALL NXTCHR
 CP ' '
 JR NC,COMDAI1
 LD A,(CNSTIF+1)
 OR A
 JR NZ,COMDAI2
 INC A
COMDAI2 LD C,A
 LD B,0
 POP HL
 ADD HL,BC
 JR COMDAILP
COMDAI1 CALL ASMLINI
 POP HL
 JR Z,COMDAILP
 LD (DISADR),HL
 PUSH HL
 LD A,2
 LD (ASMTYP),A
 LD HL,ASMBUF
 LD DE,INPBUF+128
 LD BC,128
 PUSH DE
 LDIR 
 POP HL
 CALL ASMTOBUF
 POP HL
 JR Z,COMDAILP
 LD HL,(DISADR)
 CALL BUFTOMEM
 JR COMDAILP
;
LBMASKMS DEFM 'Label mask : '
 NOP 
;
COMDCS LD HL,ORIGMS
 CALL COMDCSSB
 RET Z
 CALL FDLBLS
 CALL Z,RSTMEM
 JP Z,DISBAD
 PUSH BC
 LD HL,NEWMS
 CALL COMDCSSB
 POP BC
 JP Z,RSTMEM
 PUSH BC
 CALL FDLBLS
 POP HL
 CALL NZ,RSTMEM
 JP NZ,DISBAD
 CALL LOCLBL
 EX DE,HL
 LD HL,LBLBUF
 LD BC,8
 LDIR 
 JP RSTMEM
;
COMDCSSB CALL MESAGE
 LD HL,INPBUF
 LD B,8
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CALL SKPLBL
 JP Z,DISBAD
 LD A,(HL)
 CP 33
 JP NC,DISBAD
 RET 
;
ORIGMS DEFM 'Original label : '
 NOP 
;
NEWMS DEFM 'New label      : '
 NOP 
;
COMDFN LD A,(FILENAME)
 OR A
 JR Z,COMDFN2
 LD HL,FILNMS1
 CALL MESAGE
 LD HL,(SOURCEXT)
 LD A,(SOURCEXT+2)
 LD (EXTENS),HL
 LD (EXTENS+2),A
 CALL DSFILNAM
COMDFN2 LD HL,FILNMS2
 CALL MESAGE
 LD HL,INPBUF
 LD B,16
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CALL PARSFNAM
 LD HL,5341H
 LD A,'M'
 BIT 4,C; EXT WILDCARD?
 JR NZ,COMDFN3
 BIT 2,C; EXT GEDEF?
 JR Z,COMDFN3
 LD HL,(FCB+9)
 LD A,(FCB+11)
COMDFN3 LD (SOURCEXT),HL
 LD (SOURCEXT+2),A
 LD A,C
 AND 0AH
 CP 2
 JR NZ,COMDFN1
 INC A
 LD HL,FCB
 LD DE,DRIVENO
 LD BC,9
 LDIR 
 RET 
COMDFN1 XOR A
 LD (FILENAME),A
 JP DISBAD
;
FILNMS1 DEFM 'Old file name: '
 NOP 
;
FILNMS2 DEFM 'New file name? '
 NOP 
;
PPFCBO CALL PRPFCBSB
 LD HL,4F43H
 LD A,'M'
 JR PRPFCB
;
PPFCBA CALL PRPFCBSB
 LD HL,(SOURCEXT); 5341H
 LD A,(SOURCEXT+2); 'M'
;
PRPFCB RET Z
 LD (EXTENS),HL
 LD (EXTENS+2),A
 LD HL,DRIVENO
 LD DE,FCB
 PUSH DE
 LD B,35
 XOR A
PPFCB1 LD (DE),A
 INC DE
 DJNZ PPFCB1
 POP DE
 LD BC,12
 LDIR 
 DEC A
 RET 
;
PRPFCBSB LD DE,(TRADDR)
 LD C,1AH
 CALL BDOS
 LD A,(FILENAME)
 OR A
 CALL Z,COMDFN
 RET 
;
OPEN CALL PPFCBA
 JR NZ,OPEN1
OPEN2 CP 3
 RET NZ
 OR A
 RET 
OPEN1 LD A,255
 LD (DSKCNT),A
 LD C,15
 LD HL,READMES
OPEN3 PUSH BC
 CALL MESAGE
 CALL DSFILNAM
 POP BC
 LD DE,FCB
 CALL BDOS
 OR A
 RET Z
 LD HL,NOTFNDMS
 CALL MESAGE
 INC A
 RET 
;
DSFILNAM LD A,(DRIVENO)
 OR A
 JR Z,DSFILNM1
 ADD A,64
 CALL CHPUT1
 LD A,':'
 CALL CHPUT1
DSFILNM1 LD HL,FILENAME
 LD B,8
DSFILNM2 LD A,(HL)
 CP ' '
 JR Z,DSFILNM3
 CALL CHPUT1
 INC HL
 DJNZ DSFILNM2
DSFILNM3 LD A,'.'
 CALL CHPUT1
 LD HL,EXTENS
 LD B,3
DSFILNM4 LD A,(HL)
 CALL CHPUT1
 INC HL
 DJNZ DSFILNM4
 LD A,13
 JP CHPUT1
;
READMES DEFM 'Reading : '
 NOP 
;
CREATE JR Z,OPEN2
 XOR A
 LD (DSKCNT),A
 LD C,16H
 LD HL,WRITEMES
 JR OPEN3
;
WRITEMES DEFM 'Writing : '
 NOP 
;
INPDSK PUSH BC
 PUSH DE
 PUSH HL
 LD A,(DSKCNT)
 CP 128
 JR C,IPDSK1
 CP 254
 JR NZ,IPDSK2
 LD A,1AH
 JR IPDSK3
IPDSK2 LD C,14H
 LD DE,FCB
 CALL BDOS
 OR A
 JR Z,IPDSK1
 LD A,254
 LD (DSKCNT),A
 LD A,(FILETYPE)
 OR A
 CALL NZ,CLRSRC
 LD HL,UNXPECMS
 CALL MESAGE
 JP MAINLP
IPDSK1 LD C,A
 INC A
 LD (DSKCNT),A
 LD B,0
 LD HL,(TRADDR)
 ADD HL,BC
 LD A,(HL)
IPDSK3 POP HL
 POP DE
 POP BC
 RET 
;
UNXPECMS DEFB 7
 DEFM '*** Unexpected end of file'
 DEFW 13
;
OUTDSK PUSH BC
 PUSH DE
 PUSH HL
 PUSH AF
 LD A,(DSKCNT)
 CP 128
 JR C,OTDSK1
 LD C,15H
 LD DE,FCB
 CALL BDOS
 OR A
 JR Z,OTDSK1
DSKFULER LD HL,DSKFULMS
 CALL MESAGE
 JP MAINLP
OTDSK1 LD C,A
 INC A
 LD (DSKCNT),A
 LD B,0
 LD HL,(TRADDR)
 ADD HL,BC
 POP AF
 LD (HL),A
 POP HL
 POP DE
 POP BC
 RET 
;
DSKFULMS DEFB 7
 DEFM '*** Disk full'
 DEFW 13
;
COMDRS CALL SETLINK
 XOR A
 LD (FILENAME),A
 LD (FILETYPE),A
 CALL OPEN
 RET NZ
 CALL ERASUR
 JR NZ,CMDRS8
 CALL INPDSK
 CP 128
 JR C,CMDRS9
 LD HL,ERRMS2
 JP MESAGE
CMDRS9 LD DE,-1
 CALL FNDLIN
 XOR A
 LD (FILENAME),A
 PUSH HL
 LD HL,0
 SBC HL,DE
 LD (LINCNT),HL
 POP DE
 JR CMDRSA
CMDRS8 CALL INPDSK
 CP 128
 JP NC,COMDRSC
 LD DE,1
 LD (LINCNT),DE
 CALL FNDLIN
 EX DE,HL
CMDRSA XOR A
 LD (DSKCNT),A
 LD A,WAITVAL-1
 LD (CNTWAIT),A
CMDRS3 LD HL,INPBUF+1
 PUSH DE
 PUSH HL
CMDRS1 CALL INPDSK
 CP 9
 JR NZ,CMDRSB
 LD A,' '
CMDRSB CP 1AH
 JR Z,CMDRS4
 CP ' '
 JR C,CMDRS1
 LD (HL),A
 INC HL
 LD B,80
CMDRS5 CALL INPDSK
 CP 9
 JR NZ,CMDRSC
 LD A,' '
CMDRSC LD (HL),A
 INC HL
 CP ' '
 JR C,CMDRSD
 DJNZ CMDRS5
 LD (HL),0
CMDRSD CP 1AH
 JR Z,CMDRS4
 POP HL
 PUSH HL
 LD A,13
 CALL CHPUT
 POP HL
 PUSH HL
 CALL ASMLN7
 POP HL
 JR NZ,CMDRS6
 DEC HL
 LD (HL),';'
 CALL ASMLIN
 LD HL,ASMCNT
 CALL DISSRC
CMDRS6 LD HL,ASMCNT
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 INC BC
 POP DE
 LDIR 
 XOR A
 LD (DE),A
 LD (ENDTXT),DE
 CALL INCLIN1
 JR CMDRS3
CMDRS4 POP HL
 POP DE
 JP COMDFM
;
MEMFULFE CALL MEMFULER
 CALL CLRSRC
 JP MAINLP
;
COMDRSC LD (FILETYPE),A
 CP 0FCH
 LD HL,ERRMS4
 JP NZ,MESAGE
 CALL INPDSK
 CP 2
 LD HL,ERRMS3
 JP NC,MESAGE
 LD HL,1
 CALL LOCLBL
 CALL INPDSK
 LD C,A
 CALL INPDSK
 LD B,A
 PUSH HL
 LD HL,(LABELSPC)
 CP A
 SBC HL,BC
 POP HL
 JR C,MEMFULFE
CMDRSCL1 CALL INPDSK
 LD (HL),A
 INC HL
 DEC BC
 LD A,B
 OR C
 JR NZ,CMDRSCL1
 CALL INPDSK
 LD L,A
 CALL INPDSK
 LD H,A
 LD (LBLCNT),HL
 CALL RSTMEM
 CALL INPDSK
 LD C,A
 CALL INPDSK
 LD B,A
 LD HL,(HIMEM)
 LD DE,(SRCADR)
 CP A
 SBC HL,DE
 SBC HL,BC
 JR C,MEMFULFE
 LD HL,(SRCADR)
CMDRSCL2 CALL INPDSK
 LD (HL),A
 INC HL
 DEC BC
 LD A,B
 OR C
 JR NZ,CMDRSCL2
 DEC HL
 LD (ENDTXT),HL
 JP COMDFM
;
COMDWS CALL SETLINK
 LD HL,WSQMES
 CALL MESAGE
 CALL CHGET1
 PUSH AF
 CALL CHPUT1
 LD A,13
 CALL CHPUT1
 POP AF
 CP 3
 RET Z
 CP 'A'
 JR Z,CMDWS4
 CP 'C'
 JP NZ,DISBAD
CMDWS4 PUSH AF
 CALL PPFCBA
 JR Z,CMDWS6
 PUSH AF
 LD DE,FCB
 LD C,11H
 CALL BDOS
 OR A
 JR NZ,CMDWS7
 LD HL,(FCB+9)
 LD A,(FCB+11)
 PUSH HL
 PUSH AF
 LD HL,4142H
 LD A,'K'
 LD (FCB+9),HL
 LD (FCB+11),A
 LD C,13H
 LD DE,FCB
 CALL BDOS
 LD HL,FCB
 LD DE,FCB+16
 LD BC,16
 LDIR 
 POP AF
 POP HL
 LD (FCB+9),HL
 LD (FCB+11),A
 LD C,17H
 LD DE,FCB
 CALL BDOS
 LD HL,FCB+16
 LD B,16
CMDWS8 LD (HL),0
 INC HL
 DJNZ CMDWS8
CMDWS7 POP AF
CMDWS6 CALL CREATE
 POP BC
 RET NZ
 LD A,B
 CP 'C'
 JR Z,COMDWSC
 LD DE,1
 LD (LINCNT),DE
 LD A,WAITVAL-1
 LD (CNTWAIT),A
 CALL FNDLIN
CMDWS1 LD A,(HL)
 OR A
 JR Z,CMDWS2
 PUSH HL
 CALL DISSRS
 LD HL,(BUFPNT)
 LD (HL),0
 LD HL,OUTBUF+5
 LD E,L
 LD D,H
 PUSH HL
 CALL COMPRESS
 POP HL
CMDRS2 LD A,(HL)
 CP ' '
 JR C,CMDRS7
 INC HL
 CALL OUTDSK
 JR CMDRS2
CMDRS7 LD A,0DH
 CALL OUTDSK
 LD A,10
 CALL OUTDSK
 CALL INCLIN1
 POP HL
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 ADD HL,BC
 INC HL
 JR CMDWS1
CMDWS2 LD A,1AH
 CALL OUTDSK
 LD A,' '
 LD B,4
CMDWS5 CALL CHPUT
 DJNZ CMDWS5
 LD A,13
 CALL CHPUT
CMDWS3 LD C,15H
 LD DE,FCB
 CALL BDOS
 OR A
 JP NZ,DSKFULER
 LD C,10H
 LD DE,FCB
 CALL BDOS
 OR A
 RET Z
 JP DSKFULER
;
COMDWSC LD A,0FCH
 CALL OUTDSK
 LD A,1
 CALL OUTDSK
 LD HL,1
 CALL LOCLBL
 LD BC,12
COMDWSC1 LD A,(HL)
 OR A
 JR Z,COMDWSC2
 ADD HL,BC
 JR COMDWSC1
COMDWSC2 LD DE,(LBLADR)
 CP A
 SBC HL,DE
 INC HL
 LD A,L
 CALL OUTDSK
 LD A,H
 CALL OUTDSK
 EX DE,HL
CMDWSCL1 LD A,(HL)
 CALL OUTDSK
 INC HL
 DEC DE
 LD A,D
 OR E
 JR NZ,CMDWSCL1
 CALL RSTMEM
 LD HL,(LBLCNT)
 LD A,L
 CALL OUTDSK
 LD A,H
 CALL OUTDSK
 LD HL,(ENDTXT)
 LD DE,(SRCADR)
 CP A
 SBC HL,DE
 EX DE,HL
 INC DE
 LD A,E
 CALL OUTDSK
 LD A,D
 CALL OUTDSK
CMDWSCL2 LD A,(HL)
 CALL OUTDSK
 INC HL
 DEC DE
 LD A,D
 OR E
 JR NZ,CMDWSCL2
 JR CMDWS3
;
WSQMES DEFM 'ASCII or code type (A/C)? '
 NOP 
;
INCLIN1 CALL INCLIN
 LD A,(CNTWAIT)
 DEC A
 LD (CNTWAIT),A
 RET NZ
 LD A,WAITVAL
 LD (CNTWAIT),A
;
INCLIN2 PUSH BC
 PUSH DE
 PUSH HL
 LD DE,(LINCNT)
 CALL CLRBUF
 CALL LINENO
 LD HL,OUTBUF
 LD B,4
INCLINLP LD A,(HL)
 CALL CHPUT
 INC HL
 DJNZ INCLINLP
 LD A,13
 CALL CHPUT
 POP HL
 POP DE
 POP BC
 RET 
;
INCLIN PUSH HL
 LD HL,(LINCNT)
 INC HL
 LD (LINCNT),HL
 POP HL
 RET 
;
COMDLL LD HL,1
 LD (DEFLINE),HL
 CALL ASKFLN
 CP 3
 RET Z
 LD (LINCNT),DE
 CALL FNDLINER
 RET Z
 JR CMDLC1
;
COMDLC LD DE,1
 LD (LINCNT),DE
 CALL FNDLIN
 RET Z
CMDLC1 LD A,(HL)
 AND 7FH
 RET Z
 LD C,A
 PUSH BC
 PUSH HL
 CALL DISSRC
 LD HL,(LINCNT)
 INC HL
 LD (LINCNT),HL
 CALL BREAKX
 POP HL
 POP BC
 RET C
 LD B,0
 ADD HL,BC
 INC HL
 JR CMDLC1
;
STLBL0 PUSH HL
 PUSH DE
 PUSH BC
 EX DE,HL
 CALL LOCLBL
 LD DE,9
 ADD HL,DE
 LD A,(HL)
 CP 80H
 POP BC
 PUSH AF
 OR 80H
 LD (HL),A
 INC HL
 LD (HL),C
 INC HL
 LD (HL),B
 CALL RSTMEM
 POP AF
STLBL2 POP DE
 POP HL
 RET 
;
SETLBL CALL STLBL0
 RET C
 PUSH HL
 PUSH DE
 LD HL,DBDFMS
 CALL DISERR
 JR STLBL2
;
VALLBL PUSH HL
 PUSH DE
 EX DE,HL
 CALL LOCLBL
 LD DE,9
 ADD HL,DE
 LD A,(HL)
 INC HL
 CP 80H
 JR NC,VLLBL1
 PUSH HL
 LD (HL),0
 INC HL
 LD (HL),0
 LD HL,UDSMS
 CALL DISERR
 POP HL
VLLBL1 LD C,(HL)
 INC HL
 LD B,(HL)
 POP DE
 POP HL
 JP RSTMEM
;
VALTRM LD A,(HL)
 LD E,A
 AND 0DFH
 CP 0D0H
 JR NZ,VALTR1
 LD A,E
 INC HL
 PUSH AF
 LD A,(PRIORITY)
 PUSH AF
 LD A,(THISOPR)
 PUSH AF
 CALL VALEXP
 POP AF
 LD (THISOPR),A
 POP AF
 LD (PRIORITY),A
 LD A,(ASBRACK)
 OR A
 JR Z,VALTR3
 DEC A
 LD (ASBRACK),A
 JR VALTR3
VALTR1 AND 7
 LD (NEXTOPR),A
 LD A,E
 AND 0D0H
 CP 90H
 LD A,E
 INC HL
 JR NZ,VALTR6
 LD DE,(DISADR)
 JR VALTR2
VALTR6 LD E,(HL)
 INC HL
 LD D,0
 BIT 3,A
 JR NZ,VALTR2
 LD D,(HL)
 INC HL
VALTR2 PUSH AF
 AND 0D0H
 CP 0C0H
 JR NZ,VALTR3
 CALL VALLBL
 LD E,C
 LD D,B
VALTR3 POP AF
 BIT 5,A
 JR Z,VALTR4
 CP A
 PUSH HL
 LD HL,0
 SBC HL,DE
 EX DE,HL
 POP HL
VALTR4 LD A,(NEXTOPR)
 OR A
 RET Z
 LD A,(ASBRACK)
 LD B,A
VALTR5 LD A,(HL)
 INC HL
 INC B
 CP 0D1H
 JR Z,VALTR5
 DEC HL
 DEC B
 LD A,B
 LD (ASBRACK),A
 RET 
;
OPRPRIOR DEFB 0
 DEFB 1
 DEFB 1
 DEFB 2
 DEFB 2
 DEFB 2
 DEFB 1
;
GETPRIOR PUSH HL
 PUSH BC
 LD C,A
 LD B,0
 LD HL,OPRPRIOR
 ADD HL,BC
 LD A,(HL)
 POP BC
 POP HL
 RET 
;
VALEXP LD DE,0
 XOR A
 LD (ASBRACK),A
 INC A
 LD (THISOPR),A
 LD (PRIORITY),A
VALXP1 LD A,(PRIORITY)
 LD B,A
 LD A,(THISOPR)
 CALL GETPRIOR
 CP B
 RET C
 PUSH DE
 CALL VALTRM
 LD C,E
 LD B,D
 POP DE
VALXPB LD A,(ASBRACK)
 OR A
 JR NZ,VALXP9
 LD A,(NEXTOPR)
 PUSH DE
 CALL GETPRIOR
 LD D,A
 LD A,(PRIORITY)
 CP D
 JR NC,VALXPA
 PUSH AF
 LD E,A
 LD A,(THISOPR)
 PUSH AF
 LD A,(NEXTOPR)
 LD (THISOPR),A
 CALL GETPRIOR
 LD (PRIORITY),A
 LD A,E
 LD E,C
 LD D,B
 CALL VALXP1
 LD B,D
 LD C,E
 POP AF
 LD (THISOPR),A
 POP AF
 LD (PRIORITY),A
 POP DE
 JR VALXPB
VALXPA POP DE
VALXP9 LD A,(THISOPR)
 PUSH HL
 EX DE,HL
 DEC A
 JR Z,VALXP2
 DEC A
 JR NZ,VALXP5
 CP A
 SBC HL,BC
 JR VALXP3
VALXP5 DEC A
 JR NZ,VALXP4
DIV LD A,B
 OR C
 JR NZ,DIV6
 LD HL,-1
 JR VALXP3
DIV6 PUSH HL
 CP A
 SBC HL,BC
 POP HL
 JR NC,DIV2
 LD HL,0
 JR VALXP3
DIV2 XOR A
DIVLP1 INC A
 RL C
 RL B
 JR C,DIV3
 PUSH HL
 SBC HL,BC
 POP HL
 JR NC,DIVLP1
 CCF 
DIV3 RR B
 RR C
DIV4 LD DE,0
DIVLP CP A
 SBC HL,BC
 JR NC,DIV5
 ADD HL,BC
DIV5 CCF 
 RL E
 RL D
 RR B
 RR C
 DEC A
 JR NZ,DIVLP
 EX DE,HL
 JR VALXP3
VALXP4 DEC A
 JR NZ,VALXP6
 EX DE,HL
 LD HL,0
 LD A,16
MULTLP ADD HL,HL
 RL E
 RL D
 JR NC,MULT1
 ADD HL,BC
MULT1 DEC A
 JR NZ,MULTLP
 JR VALXP3
VALXP6 DEC A
 JR NZ,VALXP7
 LD A,H
 AND B
 LD H,A
 LD A,L
 AND C
 LD L,A
 JR VALXP3
VALXP7 LD A,H
 OR B
 LD H,A
 LD A,L
 OR C
 LD L,A
 JR VALXP3
VALXP2 ADD HL,BC
VALXP3 EX DE,HL
 POP HL
 LD A,(ASBRACK)
 OR A
 RET NZ
 LD A,(NEXTOPR)
 OR A
 RET Z
 LD (THISOPR),A
 JP VALXP1
;
COMDWO CALL SETLINK
 CALL PPFCBO
 CALL CREATE
 RET NZ
 INC A
 LD (ASMTYP),A
 CALL CMDASS
 JP CMDWS3
;
COMDFM LD HL,MEMMS
 CALL MESAGE
 LD DE,(ENDTXT)
 LD HL,(HIMEM)
 CP A
 SBC HL,DE
 JR NC,COMDFM1
 LD DE,(HIMEM)
COMDFM1 LD HL,(HIMEM);STACK SPACE
 CP A
 SBC HL,DE
 PUSH DE
 CALL CMDLN1
 LD A,' '
 CALL CHPUT1
 LD A,'('
 CALL CHPUT1
 POP HL
 INC HL
 LD A,H
 CALL DISBYT
 LD A,L
 CALL DISBYT
 LD A,'-'
 CALL CHPUT1
 LD HL,(HIMEM)
 INC HL
 LD A,H
 CALL DISBYT
 LD A,L
 CALL DISBYT
 LD HL,LBLMS
 CALL MESAGE
 LD HL,(LBLCNT)
 CALL CMDLN1
 LD HL,MAXMES
 CALL MESAGE
 LD HL,(LBLMAX)
 CALL CMDLN1
 LD A,')'
 CALL CHPUT1
 LD A,13
 JP CHPUT1
;
CMDLN1 EX DE,HL
 CALL CLRBUF
 CALL DISDEC
 LD HL,OUTBUF+5
 LD (BUFPNT),HL
 JP DISBUF
;
MEMMS DEFM 'Free mem : '
 NOP 
;
LBLMS DEFW 0D29H
 DEFM '# Labels : '
 NOP 
;
MAXMES DEFM ' (Max:'
 NOP 
;
PASS1M DEFM 'Pass 1'
 DEFW 13
;
PASS2M DEFM 'Pass 2'
 DEFW 13
;
COMDLI LD DE,0FFFFH
 JR COMDAS1
;
COMDAS LD HL,1
 LD (DEFLINE),HL
 CALL ASKFLN
 CP 3
 RET Z
 PUSH DE
 CALL FNDLINER
 POP DE
 RET Z
 CALL COMDAS1
 RET C
 LD A,13
 CALL CHPUT1
 JP COMDLSB
;
COMDAS1 LD (MINLIN),DE
 XOR A
 LD (ASMTYP),A
 JP CMDASS
;
CMDASS LD HL,PASS1M
 CALL MESAGE
 XOR A
 LD (IMEDIATE),A
 LD HL,1
 CALL LOCLBL
 LD BC,3
 LD DE,9
CMDAS1 LD A,(HL)
 OR A
 JR Z,CMDAS2
 ADD HL,DE
 LD A,(HL)
 AND 7FH
 LD (HL),A
 ADD HL,BC
 JR CMDAS1
CMDAS2 CALL RSTMEM
 XOR A
 LD L,A
 LD H,A
 INC A
 LD (DISMOD),A
 LD (LINCNT),HL
 LD (DISADR),HL
 INC HL
 EX DE,HL
 CALL FNDLIN
PASS1 LD A,(HL)
 OR A
 JP Z,PASS2
 CALL INCLIN
 PUSH HL
 CP 80H
 JR C,PASS16
 INC HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 LD (LBLPT1),DE
 LD BC,(DISADR)
 CALL SETLBL
PASS16 INC HL
 PUSH HL
 CALL FORMAT
 POP HL
 JR NZ,PASS11
 INC HL
 LD A,(HL)
 INC HL
 CP 6
 JR Z,PASS15
 CP 7
 JR Z,PASS15
 LD DE,1
 OR A
 JR Z,PASS12
 INC DE
 DEC A
 JR Z,PASS12
 DEC A
 JR NZ,PASS13
 LD E,(HL)
 JR PASS12
PASS13 PUSH AF
 CALL VALEXP
 POP AF
 DEC A
 JR Z,PASS12
 DEC A
 JR NZ,PASS14
 LD (DISADR),DE
PASS15 LD DE,0
 JR PASS12
PASS14 DEC A
 JR NZ,PASS15
 LD C,E
 LD B,D
 LD DE,(LBLPT1)
 CALL STLBL0
 JR PASS15
PASS11 LD E,B
 LD D,0
PASS12 PUSH HL
 LD HL,(DISADR)
 ADD HL,DE
 LD (DISADR),HL
 POP HL
 POP HL
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 ADD HL,BC
 INC HL
 JR PASS1
PASS2 LD HL,PASS2M
 CALL MESAGE
 XOR A
 LD L,A
 LD H,A
 LD (DISADR),HL
 LD (LSTADR),HL
 INC HL
 LD (LINCNT),HL
 EX DE,HL
 CALL FNDLIN
PASS21 LD A,(HL)
 OR A
 RET Z
 PUSH HL
 CP 80H
 JR C,PASS22
 INC HL
 INC HL
PASS22 INC HL
 CALL ASMTOBUF
 LD HL,(DISADR)
 LD A,(ASMTYP)
 OR A
 JR NZ,PASS2G
 PUSH HL
 CALL CHECKTYP
 JR NZ,PASS2A
 LD A,D
 OR E
 JR Z,PASS2A
 CALL BUFADR
 LD A,' '
 CALL PUTBUF
 LD B,E
 LD HL,ASMBUF
 LD A,B
 CP 5
 JR C,PASS2E
 LD B,4
PASS2E LD A,(HL)
 CALL BUFBYT
 INC HL
 DJNZ PASS2E
 JR PASS2A
PASS2G DEC A
 JR Z,PASS2O
 PUSH HL
 CALL BUFTOMEM
 JR PASS2A
PASS2O LD A,E
 OR A
 JR Z,PASS2H
 LD B,A
 PUSH HL
 LD HL,ASMBUF
PASS2I LD A,(HL)
 CALL OUTDSK
 INC HL
 DJNZ PASS2I
 POP HL
PASS2H PUSH HL
 LD BC,(LSTADR)
 CP A
 SBC HL,BC
 JR C,PASS2A
PASS2J LD A,H
 OR L
 JR Z,PASS2A
 XOR A
 CALL OUTDSK
 DEC HL
 JR PASS2J
PASS2A POP HL
 ADD HL,DE
 LD (DISADR),HL
 LD (LSTADR),HL
 CALL CHECKTYP
 JR NZ,PASS2K
 LD HL,OUTBUF+15
 LD (BUFPNT),HL
 CALL DISBUF
 LD DE,(DISADR)
 POP HL
 PUSH HL
 PUSH DE
 CALL DISSRS
 LD A,52
 CALL NICEMESG
 LD A,1
 LD (DISMOD),A
 POP HL
 LD (DISADR),HL
PASS2K CALL INCLIN
 CALL BREAKX
 POP HL
 RET C
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 ADD HL,BC
 INC HL
 JP PASS21
;
ASMTOBUF PUSH HL
 LD A,-1
 LD (ERROR),A
 CALL CHECKTYP
 CALL Z,CLRBUF
 POP HL
 PUSH HL
 CALL FORMAT
 LD HL,ASMBUF
 LD (PASCNT),HL
 POP HL
 JR NZ,PASS23
 INC HL
 LD A,(HL)
 INC HL
 CP 6
 JR Z,PASS28
 CP 7
 JR Z,PASS28
 CP 2
 JR NZ,PASS24
 LD C,(HL)
 INC HL
 LD B,0
 PUSH BC
 LD DE,ASMBUF
 LDIR 
 POP DE
 JP PASS25
PASS24 PUSH AF
 CALL VALEXP
 POP AF
 OR A
 JR NZ,PASS26
 LD (ASMBUF),DE
 LD DE,1
 JP PASS25
PASS26 DEC A
 JR NZ,PASS27
 LD (ASMBUF),DE
 LD DE,2
 JP PASS25
PASS27 DEC A
 DEC A
 JR NZ,PASS29
 CALL CHECKTYP
 CALL Z,BUFADR
 LD HL,(DISADR)
 ADD HL,DE
 LD (DISADR),HL
PASS28 LD DE,0
 JR PASS25
PASS29 DEC A
 JR NZ,PASS28
 LD (DISADR),DE
 LD (LSTADR),DE
 JR PASS28
PASS23 PUSH BC
 LD A,(HL)
 LD E,A
 AND 0C7H
 JR NZ,PASS2L
 LD A,E
 CP 10H
 JR C,PASS2B
 CALL PASBUF
 INC HL
 CALL VALEXP
 EX DE,HL
 LD BC,(DISADR)
 SBC HL,BC
 DEC HL
 DEC HL
 LD A,L
 LD BC,128
 ADD HL,BC
 LD B,A
 LD A,H
 OR A
 JR Z,PASS2N
 LD HL,OORMS
 CALL DISERR
 LD B,0
PASS2N LD A,B
 EX DE,HL
 CALL PASBUF
 JR PASS2F
PASS2L CP 0C7H
 JR NZ,PASS2B
 INC HL
 CALL VALEXP
 LD A,E
 OR 0C7H
 CALL PASBUF
 JR PASS2F
PASS2B PUSH BC
 LD A,C
 AND 3
 JR Z,PASS2C
 PUSH AF
 CALL VALEXP
 LD A,E
 CALL PASBUF
 POP AF
 POP BC
 DEC A
 LD A,D
 JR Z,PASS2D
 DEC B
 CALL PASBUF
 JR PASS2D
PASS2C POP BC
 LD A,(HL)
 INC HL
 CALL PASBUF
PASS2D RR C
 RR C
 DJNZ PASS2B
PASS2F POP BC
 LD E,B
 LD D,0
PASS25 LD A,(ERROR)
 OR A
 RET 
;
BUFTOMEM LD A,E
 OR A
 RET Z
 LD B,A
 PUSH DE
 LD DE,ASMBUF
PASS2P LD A,(IMEDIATE)
 OR A
 JR NZ,PASS2Q
 PUSH DE
 EX DE,HL
 LD HL,(ENDTXT)
 CP A
 SBC HL,DE
 JP NC,MEMERR
 LD HL,(HIMEM)
 CP A
 SBC HL,DE
 JP C,MEMERR
 EX DE,HL
 POP DE
PASS2Q LD A,(DE)
 LD (HL),A
 INC HL
 INC DE
 DJNZ PASS2P
 POP DE
 RET 
;
CHECKTYP LD A,(ASMTYP)
 OR A
 RET NZ
 PUSH HL
 PUSH DE
 LD DE,(MINLIN)
 LD HL,(LINCNT)
 SBC HL,DE
 POP DE
 POP HL
 JP NC,RETZRO
 INC A
 RET 
;
BUFADR LD BC,(DISADR)
 LD A,B
 CALL BUFBYT
 LD A,C
 JP BUFBYT
;
PASBUF PUSH HL
 LD HL,(PASCNT)
 LD (HL),A
 INC HL
 LD (PASCNT),HL
 POP HL
 RET 
;
MEMERR LD HL,MEMERRMS
 CALL DISERR
 JP MAINLP
;
MEMERRMS DEFM '*** Memory error'
 DEFB 13
 DEFM '         Assembly aborted'
 DEFW 13
;
DISERR PUSH BC
 PUSH DE
 XOR A
 LD (ERROR),A
 LD A,(IMEDIATE)
 OR A
 JR NZ,DISER2
 PUSH HL
 LD A,BELL
 CALL CHPUT1
 LD DE,(LINCNT)
 CALL DISLNO
 POP HL
DISER2 CALL MESAGE
 CALL CLRBUF
 POP DE
 POP BC
 RET 
;
OORMS DEFM '*** Out of range'
 DEFW 13
;
DBDFMS DEFM '*** Doubly defined label'
 DEFW 13
;
UDSMS DEFM '*** Undefined label'
 DEFW 13
;
COMDED CALL ASKLIN
 CP 3
 RET Z
 LD (LINCNT),DE
COMDEDLP LD DE,(LINCNT)
 CALL FNDLINER
 RET Z
 LD (LINADR),HL
 CALL DISSRC
 CALL CHGET1
 LD HL,(LINCNT)
 CP 31
 JR NZ,COMDED1
 INC HL
 LD (LINCNT),HL
 JR COMDEDLP
COMDED1 CP 30
 JR NZ,COMDED2
 DEC HL
 LD (LINCNT),HL
 JR COMDEDLP
COMDED2 CP 3
 RET Z
 CP 'D'
 JR NZ,COMDED3
 LD HL,(LINADR)
 CALL COMDDL1
 JR COMDEDLP
COMDED3 CP 'I'
 JR NZ,COMDED4
 LD DE,(LINCNT)
 LD HL,(LINADR)
 CALL COMDIS1
 JR COMDEDLP
COMDED4 CP 'C'
 JR NZ,COMDEDLP
 LD HL,(LINADR)
 PUSH HL
 CALL DISSRS
 CALL LINEDIT
 POP HL
 CP 3
 JP Z,COMDEDLP
 CALL COMDDL1
 LD HL,(ENDTXT)
 LD DE,(LINADR)
 CP A
 SBC HL,DE
 LD C,L
 LD B,H
 INC BC
 LD A,(ASMCNT)
 AND 7FH
 LD E,A
 LD D,0
 LD HL,(ENDTXT)
 ADD HL,DE
 INC HL
 EX DE,HL
 LD HL,(ENDTXT)
 LD (ENDTXT),DE
 LDDR 
 LD HL,ASMCNT
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 INC BC
 LD DE,(LINADR)
 LDIR 
 JP COMDEDLP
;
LINEDIT XOR A
 LD (INSRTFLG),A
 LD HL,(BUFPNT)
 LD (HL),A
 LD HL,OUTBUF+5
 LD E,L
 LD D,H
 CALL COMPRESS
 XOR A
 LD (OUTBUF+75),A
 LD HL,OUTBUF
 LD DE,INPBUF
 LD BC,100
 LDIR 
COMDED51 LD HL,INPBUF
 LD B,5
COMDED5 LD A,(HL)
 CALL CHPUT
 INC HL
 DJNZ COMDED5
 LD BC,0
 PUSH HL
COMDED6 LD A,(HL)
 OR A
 JR Z,COMDED7
 CALL CHPUT
 INC C
 INC HL
 JR COMDED6
COMDED7 POP HL
COMDED8 LD A,B
 ADD A,6
 LD (CSRX),A
 CALL CHGET1
 CP 29
 JR NZ,COMDED81
 INC B
 DEC B
 JR Z,COMDED8
 DEC B
 DEC HL
 JR COMDED8
COMDED81 CP 28
 JR NZ,COMDED82
 LD A,C
 SUB B
 JR Z,COMDED8
 INC B
 INC HL
 JR COMDED8
COMDED82 CP 13
 JP Z,COMDED87
 CP 3
 JR NZ,COMDED83
 CALL EDCROUT
 LD A,3
 RET 
COMDED83 CP 8
 JR NZ,COMDED89
 INC B
 DEC B
 JR Z,COMDED8
 LD A,29
 CALL CHPUT
 DEC B
 DEC HL
 JR COMDED8A
COMDED89 CP 7FH
 JR NZ,COMDED84
COMDED8A LD A,C
 SUB B
 JR Z,COMDED8
 PUSH HL
 PUSH BC
 LD E,L
 LD D,H
 INC HL
 LD BC,100
 LDIR 
 POP BC
 POP HL
 DEC C
 CALL CMDEDSUB
 JR COMDED8
COMDED84 CP 18
 JR NZ,COMDED85
 LD A,27
 CALL CHPUT
 LD A,(INSRTFLG)
 XOR 1
 LD (INSRTFLG),A
 ADD A,'x'
 CALL CHPUT
 LD A,'4'
 CALL CHPUT
 JR COMDED8
COMDED85 CP ' '
 JR C,COMDED8
 LD D,A
 LD A,(INSRTFLG)
 OR A
 JR Z,COMDED86
COMDED88 LD A,C
 CP 70
 JP NC,COMDED8
 PUSH DE
 PUSH BC
 PUSH HL
 LD BC,100
 ADD HL,BC
 LD E,L
 LD D,H
 DEC HL
 LDDR 
 POP HL
 POP BC
 POP AF
 LD (HL),A
 INC B
 INC C
 CALL CMDEDSUB
 INC HL
 JP COMDED8
COMDED86 LD A,B
 CP C
 JR Z,COMDED88
 LD A,D
 LD (HL),A
 CALL CHPUT
 INC HL
 INC B
 JP COMDED8
COMDED87 CALL EDCROUT
 XOR A
 LD (INSRTFLG),A
 LD HL,INPBUF+5
 CALL ASMLN7
 JP Z,COMDED51
 XOR A
 RET 
;
EDCROUT LD A,13
 CALL CHPUT
 LD A,10
 CALL CHPUT
 LD A,27
 CALL CHPUT
 LD A,'x'
 CALL CHPUT
 LD A,'4'
 JP CHPUT
;
CMDEDSUB PUSH HL
CMDEDSB1 LD A,(HL)
 OR A
 JR Z,CMDEDSB2
 CALL CHPUT
 INC HL
 JR CMDEDSB1
CMDEDSB2 LD A,' '
 CALL CHPUT
 POP HL
 RET 
;
COMPRESS LD A,(HL)
 LD (DE),A
 INC HL
 INC DE
 CP ';'
 JR Z,COMPRES1
 CP '''
 JR Z,COMPRES1
 CP ' '
 RET C
 JR NZ,COMPRESS
 CALL NXTCHR
 JR COMPRESS
COMPRES1 LD A,(HL)
 LD (DE),A
 INC HL
 INC DE
 CP ' '
 JR NC,COMPRES1
 RET 
;
COMDDS CALL ASKADR
 CP 3
 RET Z
 EX DE,HL
COMDDS1 CALL DISMEM
 CALL CHGET1
 CP 3
 RET Z
 JR COMDDS1
;
COMDAM LD A,2
 LD (ASMTYP),A
 JP CMDASS
;
REGISTRS DEFB 0C9H
 DEFM 'YL'
 DEFB 0C9H
 DEFM 'YH'
 DEFB 0C9H
 DEFM 'XL'
 DEFB 0C9H
 DEFM 'XH'
 DEFB 0CCH
 DEFB 0C8H
 DEFB 0C5H
 DEFB 0C4H
 DEFB 0C3H
 DEFB 0C2H
 DEFB 0C6H
 DEFW 0C1H
;
COMDRG CALL DISREG
 LD HL,REGMES
 CALL MESAGE
 LD HL,INPBUF
 LD B,3
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 LD DE,REGTABLE
 CALL FDWORD
 JR Z,COMDRG1
 LD L,A
 LD H,0
 ADD HL,HL
 EX DE,HL
 LD HL,RAF+2
 CP A
 SBC HL,DE
 CALL ASKVAL
 RET Z
 LD (HL),E
 INC HL
 LD (HL),D
 RET 
COMDRG1 LD DE,REGISTRS
 CALL FDWORD
 JP Z,DISBAD
 LD L,A
 LD H,0
 LD DE,RNORMAL-1
 ADD HL,DE
 CP 11
 JR NZ,COMDRG2
 LD HL,FLAGSMES
 CALL MESAGE
 LD HL,INPBUF
 PUSH HL
 LD B,13
 CALL INPUT
 POP HL
 CP 3
 RET Z
COMDRG3 LD DE,FLAGTBL1
 CALL FDWORD
 JP Z,DISBAD
 CP 9
 JR C,COMDRG4
 SUB 9
 LD (INTFLAG),A
 JR COMDRG5
COMDRG4 PUSH HL
 DEC A
 SRL A
 LD L,A
 CCF 
 SBC A,A
 LD H,0
 LD BC,FLAGTBL2
 ADD HL,BC
 AND (HL)
 LD C,A
 LD A,(RAF)
 LD B,A
 LD A,(HL)
 CPL 
 AND B
 OR C
 LD (RAF),A
 POP HL
COMDRG5 CALL NXTCHR
 CP ','
 RET NZ
 CALL NXTCHI
 JR COMDRG3
COMDRG2 CALL ASKVAL
 RET Z
 LD (HL),E
 RET 
;
ASKVAL PUSH HL
 LD HL,VALMES
 CALL MESAGE
 LD HL,INPBUF
 LD B,4
 PUSH HL
 CALL INPUT
 POP DE
 POP BC
 CP 3
 RET Z
 PUSH BC
 LD HL,0
 CALL EVLCSH
 POP HL
 XOR A
 INC A
 RET 
;
REGMES DEFM 'Register : '
 NOP 
;
VALMES DEFM 'Value    : '
 NOP 
;
FLAGSMES DEFM 'Flags    : '
 NOP 
;
GOYNMES DEFM 'Go.'
 NOP 
;
FLAGTBL1 DEFB 0C3H
 DEFB 0CEH
 DEFB 'C'
 DEFB 0DAH
 DEFB 0CEH
 DEFB 'Z'
 DEFB 0D0H
 DEFB 'E'
 DEFB 0D0H
 DEFB 'O'
 DEFB 0CDH
 DEFB 0D0H
INTTBL DEFB 0C4H
 DEFB 'I'
 DEFB 0C5H
 DEFW 'I'
;
FLAGTBL2 DEFB 1
 DEFB 40H
 DEFB 4
 DEFB 80H
;
COMDSP LD DE,(RPC)
COMDSPLP PUSH DE
 EX DE,HL
 LD (RPC),HL
 CALL DISREG
 POP HL
 PUSH HL
 CALL DISMEM
 CALL CHGET1
 CP 3
 POP DE
 RET Z
 CP 'G'
 JR NZ,COMDSPP
 CALL COMDGOSB
 LD DE,(RPC)
 JR COMDSPLP
COMDSPP LD (STEPCHAR),A
 LD A,(DISNMB)
 OR A
 JR NZ,COMDSP1
 INC DE
 JP COMDSPLP
COMDSP1 LD HL,0
 LD (STEPBUF),HL
 LD (STEPBUF+2),HL
 LD B,A
 LD HL,STEPBUF
 PUSH HL
CMDSPLP3 LD A,(DE)
 LD (HL),A
 INC DE
 INC HL
 DJNZ CMDSPLP3
 POP HL
 LD A,(HL)
 CP 0E9H
 JR NZ,COMDSPT
 LD HL,(RAF-6)
 JP COMDSPW
COMDSPT CP 0DDH
 JR NZ,COMDSPU
 INC HL
 LD A,(HL)
 CP 0E9H
 JR NZ,COMDSPV
 LD HL,(RAF-8)
 JR COMDSPW
COMDSPU CP 0FDH
 JR NZ,COMDSPS
 INC HL
 LD A,(HL)
 CP 0E9H
 JR NZ,COMDSPV
 LD HL,(RAF-10)
COMDSPW LD (RPC),HL
 JP COMDSPH
COMDSPV DEC HL
 LD A,(HL)
COMDSPS CP 18H
 JR NZ,COMDSP4
COMDSP5 INC HL
 LD A,(HL)
 LD C,A
 LD B,0
 CP 80H
 JR C,COMDSP6
 DEC B
COMDSP6 LD HL,(RPC)
 ADD HL,BC
 LD (RPC),HL
 JP COMDSP7
COMDSP4 LD B,A
 AND 0C7H
 JR NZ,COMDSP8
 LD A,B
 CP 10H
 JR NZ,COMDSP9
 LD A,(RAF-1)
 DEC A
 LD (RAF-1),A
 JR NZ,COMDSP5
 JP COMDSP7
COMDSP9 JP C,COMDSP3
 AND 18H
 CALL CONDIT
 JR NZ,COMDSP5
 JP COMDSP7
COMDSP8 CP 0C4H
 JR NZ,COMDSPE
 LD A,B
 CALL CONDIT
 JR NZ,COMDSPC
 JP COMDSP7
COMDSPE CP 0C2H
 JR NZ,COMDSPF
 LD A,B
 CALL CONDIT
 JR NZ,COMDSPG
 JP COMDSP7
COMDSPF CP 0C0H
 JR NZ,COMDSPJ
 LD A,B
 CALL CONDIT
 JR NZ,COMDSPK
 JP COMDSP7
COMDSPJ CP 0C7H
 LD A,B
 JR NZ,COMDSPA
 LD A,(STEPCHAR)
 CP 'X'
 JR Z,COMDSPR
 LD A,B
 AND 38H
 LD C,A
 LD B,0
 LD HL,(RPC)
 JR COMDSPD
COMDSPA CP 0CDH
 JR NZ,COMDSPB
COMDSPC LD A,(STEPCHAR)
 CP 'X'
 JR NZ,COMDSPQ
COMDSPR LD HL,(RSP)
 LD (SPCOPY),HL
 LD HL,(RPC)
 LD A,(DISNMB)
 LD C,A
 LD B,0
 ADD HL,BC
 LD (PCCOPY),HL
 CALL COMDGOS1
 LD A,(BREAKFLG)
 OR A
 JR Z,COMDSP7
 LD DE,(PCCOPY)
 LD HL,(SPCOPY)
 DEC HL
 LD (HL),D
 DEC HL
 LD (HL),E
 LD HL,(RPC)
 JR COMDSPH
COMDSPQ LD HL,(RPC)
 INC HL
 LD C,(HL)
 INC HL
 LD B,(HL)
COMDSPD INC HL
 PUSH BC
 EX DE,HL
 LD HL,(RSP)
 DEC HL
 LD (HL),D
 DEC HL
 LD (HL),E
 LD (RSP),HL
 POP HL
 JR COMDSPH
COMDSPB CP 0C3H
 JR NZ,COMDSPI
COMDSPG INC HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 EX DE,HL
 JP COMDSPH
COMDSPI CP 0C9H
 JR NZ,COMDSP3
COMDSPK LD HL,(RSP)
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 LD (RSP),HL
 EX DE,HL
 JR COMDSPH
COMDSP3 CALL STEPSUB
COMDSP7 LD A,(DISNMB)
 LD C,A
 LD B,0
 LD HL,(RPC)
 ADD HL,BC
COMDSPH EX DE,HL
 JP COMDSPLP
;
CONDIT RRA 
 RRA 
 RRA 
 AND 7
 SRL A
 LD D,0
 JR C,CONDIT1
 DEC D
CONDIT1 OR A
 LD E,40H
 JR Z,CONDIT2
 DEC A
 LD E,1
 JR Z,CONDIT2
 DEC A
 LD E,4
 JR Z,CONDIT2
 LD E,80H
CONDIT2 LD A,(RAF)
 XOR D
 AND E
 RET 
;
REGTABLE DEFW 46C1H
 DEFW 43C2H
 DEFW 45C4H
 DEFW 4CC8H
 DEFW 58C9H
 DEFW 59C9H
;
 DEFW 27C1H
 DEFW 27C2H
 DEFW 27C4H
 DEFW 27C8H
 DEFW 50D3H
 DEFW 43D0H
 NOP 
;
STEPSUB LD (STACKPTR),SP
 LD A,(INTFLAG)
 AND 1
 LD A,0F3H
 JR Z,COMDSPN
 LD A,0FBH
COMDSPN LD (INTBUF),A
 DI 
 LD SP,RSP+2
 POP HL
 POP DE
 POP BC
 POP AF
 EXX 
 EX AF,AF'
 POP IY
 POP IX
 POP HL
 POP DE
 POP BC
 POP AF
 LD SP,(RSP)
 JP INTBUF
COMDSP2 PUSH AF
 LD A,I
 LD A,0
 JP PO,COMDSPO
 INC A
COMDSPO LD (INTFLAG),A
 POP AF
 LD (RSP),SP
 DI 
 LD SP,RAF+2
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 PUSH IX
 PUSH IY
 EXX 
 EX AF,AF'
 PUSH AF
 PUSH BC
 PUSH DE
 PUSH HL
 LD SP,(STACKPTR)
 EI 
 RET 
;
FMES DEFM 'F : '
 NOP 
;
DISREG LD HL,FMES
 CALL MESAGE
 LD A,(RAF)
 LD B,A
 LD C,0
 LD DE,FLAGTBL2
COMDSPL LD A,(DE)
 AND B
 JR Z,COMDSPM
 LD A,0FFH
COMDSPM INC A
 ADD A,C
 LD HL,FLAGTBL1
 CALL DISTBLIM
 LD A,','
 CALL CHPUT1
 INC DE
 INC C
 INC C
 LD A,C
 CP 8
 JR NZ,COMDSPL
 LD A,(INTFLAG)
 AND 1
 LD HL,INTTBL
 CALL DISTBLIM
 LD A,13
 CALL CHPUT1
 LD DE,REGTABLE
 LD HL,RAF+1
 LD B,2
CMDSPLP1 PUSH BC
 LD B,6
CMDSPLP2 LD A,(DE)
 AND 7FH
 CALL CHPUT1
 INC DE
 LD A,(DE)
 CALL CHPUT1
 INC DE
 LD A,' '
 CALL CHPUT1
 LD A,(HL)
 CALL DISBYT
 DEC HL
 LD A,(HL)
 CALL DISBYT
 DEC HL
 LD A,' '
 CALL CHPUT1
 DJNZ CMDSPLP2
 LD A,13
 CALL CHPUT1
 POP BC
 DJNZ CMDSPLP1
 RET 
;
COMDGO CALL COMDGOSB
 RET NZ
 JP DISREG
;
COMDGOSB LD HL,GOYNMES
 CALL MESAGE
 LD HL,PROCEEDM
 CALL ASKYESNO
 CP 'Y'
 RET NZ
 LD A,0C3H
 LD (STEPBUF),A
 LD HL,(RPC)
 LD (STEPBUF+1),HL
COMDGOS1 LD A,0C3H
 LD (20H),A
 LD HL,BKHANDLE
 LD (21H),HL
 XOR A
 LD (BREAKFLG),A
 LD HL,BRKPTTBL
 LD A,(BRKPTCNT)
 OR A
 JR Z,COMDGO1
 LD B,A
COMDGO2 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 LD A,(DE)
 LD (HL),A
 LD A,0E7H
 LD (DE),A
 INC HL
 DJNZ COMDGO2
COMDGO1 CALL STEPSUB
 LD HL,BRKPTTBL
 LD A,(BRKPTCNT)
 OR A
 JR Z,COMDGO3
 LD B,A
COMDGO4 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 LD A,(HL)
 LD (DE),A
 INC HL
 DJNZ COMDGO4
COMDGO3 LD HL,BRKPTMS2
 LD A,(BREAKFLG)
 OR A
 RET Z
 JP MESAGE
;
BRKPTMS2 DEFW 90DH
 DEFM 'BREAK'
 DEFW 0D0DH
 NOP 
;
BKHANDLE LD (TEMPMEM),HL
 POP HL
 DEC HL
 LD (RPC),HL
 PUSH AF
 XOR A
 DEC A
 LD (BREAKFLG),A
 POP AF
 LD HL,(TEMPMEM)
 JP COMDSP2
;
COMDMB CALL ASKFLN
 CP 3
 RET Z
 CALL FNDLINER
 RET Z
 PUSH HL
 CALL ASKLLN
 POP HL
 CP 3
 RET Z
 PUSH HL
 CALL FNDLINER
 POP DE
 RET Z
 LD A,(HL)
 AND 7FH
 LD C,A
 LD B,0
 ADD HL,BC
 INC HL
 PUSH DE
 CP A
 SBC HL,DE
 POP DE
 JP C,DISBAD
 JP Z,DISBAD
 PUSH HL
 PUSH DE
 CALL ASKILN
 POP BC
 POP HL
 CP 3
 RET Z
 PUSH HL
 PUSH BC
 CALL FNDLINER
 POP DE
 POP BC
 RET Z
 EX DE,HL
 PUSH HL
 ADD HL,BC
 CP A
 SBC HL,DE
 POP HL
 JP Z,DISBAD
 JR C,MOVEUP
 PUSH HL
 CP A
 SBC HL,DE
 POP HL
 JP C,DISBAD
 JP Z,DISBAD
;
MOVEDWN PUSH HL
 CP A
 SBC HL,DE
 LD (BLOKLN),HL
 POP HL
MOVEDWLP INC B
 DEC B
 JR Z,MOVEDWN1
 PUSH BC
 PUSH DE
 PUSH HL
 LD BC,256
 CALL MOVEDWN2
 POP HL
 POP DE
 POP BC
 DEC B
 INC H
 INC D
 JR MOVEDWLP
MOVEDWN1 INC C
 DEC C
 RET Z
;
MOVEDWN2 PUSH DE
 PUSH BC
 PUSH HL
 LD DE,INPBUF
 LDIR 
 EX DE,HL
 POP HL
 DEC DE
 DEC HL
 LD BC,(BLOKLN)
 LDDR 
 POP BC
 POP DE
 LD HL,INPBUF
 LDIR 
 RET 
;
MOVEUP ADD HL,BC
 PUSH HL
 PUSH DE
 EX DE,HL
 CP A
 SBC HL,DE
 LD (BLOKLN),HL
 POP DE
 POP HL
 DEC HL
MOVEUPLP INC B
 DEC B
 JR Z,MOVEUP1
 PUSH BC
 PUSH DE
 PUSH HL
 LD BC,256
 CALL MOVEUP2
 POP HL
 POP DE
 POP BC
 DEC B
 DEC H
 DEC D
 JR MOVEUPLP
MOVEUP1 INC C
 DEC C
 RET Z
;
MOVEUP2 PUSH DE
 PUSH BC
 PUSH HL
 LD DE,INPBUF+255
 LDDR 
 EX DE,HL
 POP HL
 INC DE
 INC HL
 LD BC,(BLOKLN)
 LDIR 
 POP BC
 POP DE
 DEC DE
 LD HL,INPBUF+255
 LDDR 
 RET 
;
COMDMM CALL ASKADR
 CP 3
 RET Z
COMDMMLP LD A,D
 CALL DISBYT
 LD A,E
 CALL DISBYT
 LD A,' '
 CALL CHPUT1
 LD A,(DE)
 CALL DISBYT
 LD A,' '
 CALL CHPUT1
 LD B,2
 LD HL,INPBUF
 PUSH HL
 CALL INPUT
 POP HL
 CP 3
 RET Z
 CALL NXTCHR
 CALL HEXCHR
 JR C,COMDMM1
 PUSH DE
 EX DE,HL
 LD HL,0
 CALL EVLCSH
 DEC HL
 CALL NXTCHR
 CP 'H'
 CALL Z,NXTCHI
 POP HL
 CP ' '
 JR NC,COMDMM1
 LD (HL),E
 EX DE,HL
COMDMM1 INC DE
 JR COMDMMLP
;
COMDMD CALL ASKADR
 CP 3
 RET Z
COMDMDLP LD A,D
 CALL DISBYT
 LD A,E
 CALL DISBYT
 LD A,' '
 CALL CHPUT1
 LD B,16
 PUSH DE
COMDMDL1 LD A,(DE)
 CALL DISBYT
 INC DE
 LD A,B
 CP 9
 LD A,' '
 CALL Z,CHPUT1
 CALL CHPUT1
GENERR1 DJNZ COMDMDL1
 POP DE
 LD B,16
COMDMDL2 LD A,(DE)
 AND 7FH
 CP 7FH
 JR Z,COMDMD2
 CP ' '
 JR NC,COMDMD1
COMDMD2 LD A,'.'
COMDMD1 CALL CHPUT1
 LD A,B
 CP 9
 LD A,' '
 CALL Z,CHPUT1
 INC DE
 DJNZ COMDMDL2
 LD A,13
 CALL CHPUT1
 CALL CHGET1
 CP 3
 RET Z
 JR COMDMDLP
;
COMDPT LD A,(PRINTER)
 INC A
 AND 1
 LD (PRINTER),A
 LD HL,PRINTMS1
 JP Z,MESAGE
 LD HL,PRINTMS2
 JP MESAGE
;
PRINTMS1 DEFM 'Printer off'
 DEFW 13
;
PRINTMS2 DEFM 'Printer on'
 DEFW 13
;
HELLOMES DEFB 27
 DEFM 'x5'
 DEFB 27
 DEFB 'E'
 DEFM 'RF-ASSEMBLER for MSX-2 version 2.4'
 DEFB 0DH
 DEFM 'By Rolf Fokkens - 1988'
 DEFW 0D0DH
 DEFM 'RAM : '
 NOP 
;
MES64K DEFM '64 kBytes '
 DEFW 0D0DH
 NOP 
;
MESMAP DEFM 'Memory mapper'
 DEFW 0D0DH
 NOP 
;
COMMND DEFM 'CC'
 DEFW COMDCC
 DEFM 'LC'
 DEFW COMDLC
 DEFM 'IS'
 DEFW COMDIS
 DEFM 'DL'
 DEFW COMDDL
 DEFM 'LL'
 DEFW COMDLL
 DEFM 'DM'
 DEFW COMDDM
 DEFM 'FN'
 DEFW COMDFN
 DEFM 'RS'
 DEFW COMDRS
 DEFM 'CP'
 DEFW COMDCP
 DEFM 'WS'
 DEFW COMDWS
 DEFM 'AS'
 DEFW COMDAS
 DEFM 'WO'
 DEFW COMDWO
 DEFM 'FM'
 DEFW COMDFM
 DEFM 'LI'
 DEFW COMDLI
 DEFM 'ED'
 DEFW COMDED
 DEFM 'DS'
 DEFW COMDDS
 DEFM 'AM'
 DEFW COMDAM
 DEFM 'SP'
 DEFW COMDSP
 DEFM 'MB'
 DEFW COMDMB
 DEFM 'RG'
 DEFW COMDRG
 DEFM 'MM'
 DEFW COMDMM
 DEFM 'QT'
 DEFW COMDQT
 DEFM 'MD'
 DEFW COMDMD
 DEFM 'PT'
 DEFW COMDPT
 DEFM 'DR'
 DEFW COMDDR
 DEFM 'LS'
 DEFW COMDLS
 DEFM 'CS'
 DEFW COMDCS
 DEFM 'AI'
 DEFW COMDAI
 DEFM 'BK'
 DEFW COMDBK
 DEFM 'CB'
 DEFW COMDCB
 DEFM 'GO'
 DEFW COMDGO
 DEFM 'CV'
 DEFW COMDCV
HELPFUNC DEFM 'HP'
 DEFW COMDHP
 NOP 
;
RESLINK LD HL,(DISKLINK)
 LD DE,(LINKCOPY)
 LD (HL),E
 INC HL
 LD (HL),D
 RET 
;
SETLINK LD HL,(DISKLINK)
 LD E,(HL)
 INC HL
 LD D,(HL)
 LD (LINKCOPY),DE
 LD DE,ERRHND
 LD (HL),D
 DEC HL
 LD (HL),E
 LD A,-1
 LD (LINKFLAG),A
 RET 
;
ERRHND LD HL,ERRHND2
 PUSH HL
 LD HL,(LINKCOPY)
 JP (HL)
ERRHND2 PUSH AF
 LD A,C
 CP 2
 JR Z,ERRHND1
 POP AF
 RET 
ERRHND1 LD SP,(STACK)
 CALL ROMDI
 CALL MAIN3
 JP MAINLP
;
ERRMS1 DEFB 13
 DEFM '*** Printing aborted'
 DEFW 0DH
;
ERRMS2 DEFM '*** Bad file mode'
 DEFW 0DH
;
ERRMS3 DEFM '*** Bad version'
 DEFW 0DH
;
ERRMS4 DEFM '*** No source file'
 DEFW 0DH
;
MAIN3 LD DE,BUF
 LD (TRADDR),DE
 LD C,1AH
 CALL BDOS
 XOR A
 LD (PRINTER),A
 RET 
;
; HOOFD PROGRAMMA
;
MAINLP LD SP,(STACK)
 LD A,(LINKFLAG)
 OR A
 CALL NZ,RESLINK
 XOR A
 LD (LINKFLAG),A
 LD A,'?'
 CALL CHPUT1
 LD A,' '
 CALL CHPUT1
MAINL1 CALL CHGET1
 LD C,A
 CP ' '
 JR C,MAINL1
 CALL CHPUT1
 CALL CHGET1
 CP ' '
 JR NC,MAIN1
 LD A,8
 CALL CHPUT1
 JR MAINL1
MAIN1 CALL CHPUT1
 LD B,A
 LD A,13
 CALL CHPUT1
 LD HL,0
 LD (DEFLINE),HL
 LD HL,COMMND
MAINL2 LD A,(HL)
 OR A
 JR Z,MAINLP
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 EX DE,HL
 SBC HL,BC
 EX DE,HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 JR NZ,MAINL2
 LD BC,MAINLP
 PUSH BC
 PUSH DE
 RET 
;
ENTRY PUSH BC
 PUSH DE
 PUSH HL
 PUSH AF
 LD HL,(TRADDR)
 LD A,H
 CALL DISBYT
 LD A,L
 CALL DISBYT
 POP AF
 POP HL
 POP DE
 POP BC
 RET 
;
COMDQT LD HL,QUITMES
 CALL MESAGE
 LD HL,PROCEEDM
 CALL ASKYESNO
 CP 'Y'
 RET NZ
 JP 0
;
QUITMES DEFM 'Quit.'
 NOP 
;
END NOP 
;
HELPPGCT NOP 
HELPPGAD DEFS 20
HELPHLP DEFS 2
;
MOVEHELP LD HL,FREEMEM
 LD DE,HELPINFO
 LDIR 
 CALL CLRSRC
 CALL COMDFM
 JP MAINLP
;
LDIRVM RST 30H
 NOP 
 DEFW 5CH
 RET 
;
CLS LD A,27
 CALL CHPUT1
 LD A,'E'
 JP CHPUT1
;
COMDHP LD A,(HELPPGCT)
 LD B,A
 LD C,0
COMDHP1 PUSH BC
 LD B,0
 LD HL,HELPPGAD
 ADD HL,BC
 ADD HL,BC
 LD A,(HL)
 INC HL
 LD H,(HL)
 LD L,A
 LD BC,HELPINFO
 ADD HL,BC
 CALL CLS
 LD A,24
 LD (CSRY),A
 LD DE,-80
 LD (HELPHLP),DE
COMDHP7 LD DE,(TRADDR)
 LD C,0
COMDHP2 LD A,(HL)
 OR A
 JR Z,COMDHP3
 LD B,1
 INC HL
 CP 128
 JR C,COMDHP4
 SUB 80H
 LD B,A
 LD A,(HL)
 INC HL
COMDHP4 CP 13
 JR NZ,COMDHP5
 PUSH HL
 LD B,0
 LD HL,(HELPHLP)
 LD DE,80
 ADD HL,DE
 LD (HELPHLP),HL
 EX DE,HL
 LD HL,(TRADDR)
 CALL LDIRVM
 POP HL
 JR COMDHP7
COMDHP5 LD (DE),A
 INC DE
 INC C
 DJNZ COMDHP4
 JR COMDHP2
COMDHP3 POP BC
COMDHP6 CALL CHGET1
 CP 3
 JP Z,CLS
 SUB '0'
 JR C,COMDHP6
 CP B
 JR NC,COMDHP6
 LD C,A
 JR COMDHP1
 JR COMDHP
;
HELPINFO NOP 
;
INIT LD HL,(6)
 DEC HL
 LD (STACK),HL
 LD SP,HL
 DEC H
 LD (HIMEM),HL
 XOR A
 CALL CHGCAP
 LD A,80
 LD (0F3AEH),A
 XOR A
 CALL CHGMOD
 LD A,-1
 LD (0FCABH),A
 CALL RSTMEM
 XOR A
 LD (BRKPTCNT),A
 LD (4000H),A
 LD A,6
 OUT (0FDH),A
 LD (4000H),A
 CALL RSTMEM
 LD A,(4000H)
 OR A
 JR Z,TYPE128K
TYPE64K LD A,2
 LD (LBLPAGE),A
 LD HL,END
 LD (LBLADR),HL
 LD HL,900
 LD (LBLMAX),HL
 LD HL,END+LBLSP64
 LD (SRCADR),HL
 LD HL,LBLSP64
 LD (LABELSPC),HL
 LD HL,MES64K
 JR INIT4
TYPE128K LD A,6
 LD (LBLPAGE),A
 LD HL,4000H
 LD (LBLADR),HL
 LD HL,1200
 LD (LBLMAX),HL
 LD HL,END
 LD (SRCADR),HL
 LD HL,LBLSP128
 LD (LABELSPC),HL
 LD HL,MESMAP
INIT4 PUSH HL
 CALL MAIN3
 LD HL,HELPMES
 CALL ASKYESNO
 CP 'Y'
 JR NZ,INIHELP4
 LD HL,HELPFILE
 LD DE,DRIVENO
 LD BC,9
 LDIR 
 LD HL,(HELPFILE+9)
 LD A,(HELPFILE+11)
 LD (SOURCEXT),HL
 LD (SOURCEXT+2),A
 CALL OPEN
 JP NZ,INIHELP4
 CALL INPDSK
 LD (HELPPGCT),A
 INC A
 ADD A,A
 LD B,A
 LD HL,HELPPGAD
INIHELP1 CALL INPDSK
 LD (HL),A
 INC HL
 DJNZ INIHELP1
 DEC HL
 LD B,(HL)
 DEC HL
 LD C,(HL)
 INC BC
 LD HL,HELPINFO-END
 ADD HL,BC
 PUSH HL
 LD HL,FREEMEM
INIHELP2 CALL INPDSK
 LD (HL),A
 INC HL
 DEC BC
 LD A,B
 OR C
 JR NZ,INIHELP2
 POP BC
 LD HL,(SRCADR)
 ADD HL,BC
 LD (SRCADR),HL
 LD A,(LBLPAGE)
 CP 2
 JR NZ,INIHELP4
 LD HL,(LBLADR)
 ADD HL,BC
 LD (LBLADR),HL
INIHELP4 XOR A
 LD (FILENAME),A
 LD HL,HELLOMES
 CALL MESAGE
 POP HL
 CALL MESAGE
 LD HL,(HIMEM)
 LD (RSP),HL
 XOR A
 LD (FILENAME),A
 LD A,(HELPPGCT)
 OR A
 JP NZ,MOVEHELP
 LD (HELPFUNC),A
 CALL CLRSRC
 CALL COMDFM
 JP MAINLP
;
HELPMES DEFB 27
 DEFM 'x5'
 DEFM 'Include help-pages (Y/N)? '
 NOP 
;
HELPFILE NOP 
 DEFM 'RFASSEM HLP'
;
FREEMEM NOP 
ELPMES DEFB 27
 DEFM